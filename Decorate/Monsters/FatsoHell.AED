//===========================================================================
//
// Mancubus
//
//===========================================================================
ACTOR DEFatsoHell : EDE_Mancubus
{
	//$Angled
	//$Category "DE base monsters"
	//$Color 12
	//$Title "Hell Mancubus"
	Health 2000
	Radius 48
	Height 64
	Mass 2000
	Speed 8
	PainChance 80
	Monster
	MeleeRange 256	//for the flamethrower attack
	+NEVERRESPAWN
	+FLOORCLIP
	+BOSSDEATH
	+NoTarget
	+NoInFighting
	SeeSound	"Doom4/Monsters/Mancubus/Sight"
	PainSound	"Doom4/Monsters/Mancubus/Pain"
	DeathSound	"Doom4/Monsters/Mancubus/Death"
	ActiveSound	"Doom4/Monsters/Mancubus/Idle"
	Obituary "$OB_FATSO"
	Scale 1.08
	Species "Fatso"
	DamageFactor "Hazard", 0
	Tag "Mancubus"
	DropItem "ZombieTimer", 192
	DropItem "ZombieTimer", 48
	DropItem "HeavyMediumDrop"
	States
	{
	Spawn:
		FATA A 0 NODELAY 
		Goto Idle
	Idle:
		CRIS DD 15 A_Look
		Loop
	See:
		CRIS ABCDEFGHIJ 3 A_Chase
		Loop
		
	Melee:
		TNT1 A 0 A_Jump(64,"Missile") // 1/2 of the times, it will still go to missile
		TNT1 A 0 A_JumpIfCloser(128,"Shockwave")
		Goto Flamethrower
	Shockwave:
		TNT1 A 0 A_Jump(64,"Missile")
		FATA B 0 A_Playsound("Mancubus/GroundPound",6,1.0,false,1.0)
		FATA B 1 A_FaceTarget
		FATA B 0 A_SpawnItemEx("FatsoShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		FATA B 0 A_Explode(70,256,0)
		TNT1 A 0 A_Quake(6,35,0,272,0)
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardGas",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		TNT1 AAAAAAAAA 0 A_SpawnItemEx("FatsoHazardPool",random(160,-160),random(160,-160),0,0,0,0,random(0,359))
		
		FATA B 0 A_RadiusThrust(4000,128,RTF_NOIMPACTDAMAGE|RTF_NOTMISSILE)
		FATA B 20
		Goto See
			
	//------------------
	// Flamethrower	
	FlameThrower:
		FATA A 5 A_FaceTarget
		FATA B 5 A_FaceTarget
		FATA B 0 A_PlaySound("Doom4/Other/FlamerStart",6,1.0,false,1.2)
		Goto FireLoop
	FireLoop:
		FATA C 0  Bright A_JumpIfCloser(290,"Fire")
		FATA C 0  Bright A_JumpIfCloser (160,"Shockwave")
		Goto EndFlameThrower
	Fire:
		FATA C 0 A_FaceTarget
		FATA C 0  Bright A_JumpIfCloser (160,"Shockwave")
		FATA C 1 A_CustomMissile("FatsoFlamethrower",20,24)
		FATA C 0  Bright A_JumpIfCloser (160,"Shockwave")
		FATA C 1 A_CustomMissile("FatsoFlamethrower",20,-24)
		FATA C 0  Bright A_JumpIfCloser (160,"Shockwave")
		FATA C 0 A_CPosRefire
		Goto FireLoop
	EndFlameThrower:
		FATA DEBA 4
		Goto See
			
	Missile:
		FATA A 0 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA A 6 A_PlaySound("Doom4/Monsters/MancubusMortar",CHAN_AUTO,1.0,false,1.1)
		FATA F 2 BRIGHT A_CustomMissile("D4FatsoFire",20,-28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA EA 2 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA G 4 BRIGHT A_CustomMissile("D4FatsoFire",20,28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		TNT1 A 0 A_Jump(64,"See")
		FATA EA 4 A_FaceTarget
		FATA F 2 BRIGHT A_CustomMissile("D4FatsoFire",20,-28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA EA 2 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA G 4 BRIGHT A_CustomMissile("D4FatsoFire",20,28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		TNT1 A 0 A_Jump(64,"See")
		FATA EA 4 A_FaceTarget
		FATA F 2 BRIGHT A_CustomMissile("D4FatsoFire",20,-28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA EA 2 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		FATA G 4 BRIGHT A_CustomMissile("D4FatsoFire",20,28)
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		TNT1 A 0 A_Jump(64,"See")
		FATA EA 4 A_FaceTarget
		FATA EA 4 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(190,"Shockwave")
		Goto See
	Pain:
		FAPP A 3
		FAPP B 3 A_Pain
		Goto See
	Death:
		SMAN A 3
		SMAN B 3 A_Scream
		SMAN CDE 3
		SMAN F 3 A_Noblocking
		SMAN GHIJKLMNOP 3
		SMAN Q 3 A_Bossdeath
		"####" "#" 30
		"####" "#" 0 A_CustomMissile ("GrowingBloodPool", 0, 0, random (0, 360), 2, random (0, 90))
		SMAN QQQQQ 20
	    Goto XDeath
	
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 AA 0 A_SpawnItemEx("BloodGibDropperBig",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 AA 0 A_SpawnItemEx("BloodGibDropperBigMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	}
}

Actor FatsoHazardGas
{
	+NOCLIP
	+FloorHugger
	-NOGRAVITY
	Height 20
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1.8,1.1,0.1]"
	XScale 0.8
	YScale 0.5
	Alpha 0.0
	DamageType "Hazard"
	+NoInfighting
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		X110 A 0	
		X110 ABCDEFGHIJKLMNOP 1 Bright A_Fadeto(0.8,0.05)	
		X110 QRSTUVWXY 1 Bright  A_Explode(random(1,2),32,0,0,32) 							//up to here, 50 tics
		X110 ABCDEFGHIJKLMNOPQR 1 Bright 	
		X110 QRSTUVWXY 1 Bright  A_Explode(random(1,2),32,0,0,32) 							//up to here, 50 tics						//up to here, 50 tics
		X110 ABCDEFGHIJKLMNOPQR 1 Bright //+36
		X110 RSTUVWXYZABCDE 1 Bright A_FadeOut(0.076)			//26
		Stop
	}
}



Actor FatsoHazardPool
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1.8,1.1,0.1]"
	Scale 1.8
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"B11","B12","B13","B14","B21","B22","B23","B24","B31","B32","B33","B34")
	B11:
		X057 ABCDEF 2 Bright A_FadeOut(0.05)
		X057 GHIJK 1 Bright A_FadeOut(0.05)
		X057 LMNOPQRS 1 Bright A_FadeOut(0.03)
		Wait
	B12:
		XA57 ABCDEF 2 Bright A_FadeOut(0.05)
		XA57 GHIJK 1 Bright A_FadeOut(0.05)
		XA57 LMNOPQRS 1 Bright A_FadeOut(0.03)
		Wait
	B13:
		XB57 ABCDEF 2 Bright A_FadeOut(0.05)
		XB57 GHIJK 1 Bright A_FadeOut(0.05)
		XB57 LMNOPQRS 1 Bright A_FadeOut(0.03)
		Wait
	B14:
		XC57 ABCDEF 2 Bright A_FadeOut(0.05)
		XC57 GHIJK 1 Bright A_FadeOut(0.05)
		XC57 LMNOPQRS 1 Bright A_FadeOut(0.03)
		Wait
	B21:
		X058 ABCDEF 2 Bright A_FadeOut(0.05)
		X058 GHIJK 1 Bright A_FadeOut(0.05)
		X058 LMNOPQR 1 Bright A_FadeOut(0.03)
		Wait
	B22:
		XA58 ABCDEF 2 Bright A_FadeOut(0.05)
		XA58 GHIJK 1 Bright A_FadeOut(0.05)
		XA58 LMNOPQR 1 Bright A_FadeOut(0.03)
		Wait
	B23:
		XB58 ABCDEF 2 Bright A_FadeOut(0.05)
		XB58 GHIJK 1 Bright A_FadeOut(0.05)
		XB58 LMNOPQR 1 Bright A_FadeOut(0.03)
		Wait
	B24:
		XC58 ABCDEF 2 Bright A_FadeOut(0.05)
		XC58 GHIJK 1 Bright A_FadeOut(0.05)
		XC58 LMNOPQR 1 Bright A_FadeOut(0.03)
		Wait
	B31:
		X059 ABCD 2 Bright A_FadeOut(0.03)
		X059 EFGHIJ 1 Bright A_FadeOut(0.04)
		X059 KLM 1 Bright A_FadeOut(0.05)
		Wait
	B32:
		XA59 ABCD 2 Bright A_FadeOut(0.03)
		XA59 EFGHIJ 1 Bright A_FadeOut(0.04)
		XA59 KLM 1 Bright A_FadeOut(0.05)
		Wait
	B33:
		XB59 ABCD 2 Bright A_FadeOut(0.03)
		XB59 EFGHIJ 1 Bright A_FadeOut(0.04)
		XB59 KLM 1 Bright A_FadeOut(0.05)
		Wait
	B34:
		XC59 ABCD 2 Bright A_FadeOut(0.03)
		XC59 EFGHIJ 1 Bright A_FadeOut(0.04)
		XC59 KLM 1 Bright A_FadeOut(0.05)
		Wait
	}
}

//----------------------------------------
// flamethrower attack
actor FatsoFlamethrower : D4AbstractMissile
{
	var int user_counter;
	Projectile
	Radius 5
	Height 10
	RenderStyle Add
	Scale 0.6
	Speed 35
	FastSpeed 25
	Damage (3)
	+BRIGHT
	+SLIDESONWALLS
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7,8)
		DB55 ACEGI 1 
		// If collided before this, highly visible sprites are forced
		DB55 J 0 A_SetUserVar(user_counter, 1)
		DB55 KMOQSUWY 1 A_ScaleVelocity(0.95)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1))
		DB56 ACEGIK 1 A_ChangeVelocity(-0.8,0,0.3,CVF_RELATIVE)
		Stop
	Death:
		"####" "#" 0 A_JumpIf(user_counter < 1, "Crash")
		"####" "#" 1 A_FadeOut(0.14)
		Wait
	Crash:
		DB55 KMOQSUWY 1
		DB56 EFGHIJK 1 A_FadeOut(0.14)
		Stop
	}
}


//----------------------------------------
// fireball attack
Actor D4FatsoFire : D4AbstractMissile
{
	+FORCEXYBILLBOARD
	Projectile
	Speed 38
	FastSpeed 38
	Radius 5
	Height 10
	Damage 3 
	Scale 1
	RenderStyle Add
	Alpha 0.99
	SeeSound "Mancubus/Fire"
	DeathSound "Doom4/Other/FireballHit1"
	Decal "FireballScorchBig"
	States
	{
	Spawn:
		X029 A 0 NODELAY A_Jump(210,2,4,6,8)
		X029 ABCDEFGHIJK 1 Bright Light("YellowMediumFlicker2") A_SpawnItemEx("ImpBallFlamelet",random(-2,-8),random(8,-8),random(8,-8),0,0,frandom(0,0.6),0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION,100)
		Goto Spawn+1
	Death: 
		TNT1 A 0 A_SpawnItemEx("FatsoExploParticle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		TNT1 A 2 Light("YellowMediumFlicker1")
		TNT1 A 2 Light("YellowMediumFlicker2")
		TNT1 A 2 Light("YellowMediumFlicker3")
		TNT1 A 2 Light("YellowSmallFlicker1")
		TNT1 A 2 Light("YellowSmallFlicker2")
		TNT1 A 2 Light("YellowSmallFlicker3")
		Stop
	}
}

Actor FatsoExploParticle
{
	+NOINTERACTION
	+BRIGHT
	Scale 0.5
	RenderStyle Add
	Alpha 0.99
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		DB53 AABBCCDDEEFFGGHHIIJJ 1 A_SetScale(scaleX+frandom(0.0,0.05))
		DB53 JJKKKLLLMMM 1 A_FadeOut
		Stop
	}
}


Actor FatsoFireTrail
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+BRIGHT
	RenderStyle Add
	Alpha 0.35
	Scale 0.2
	Translation "0:255=%[0,0,0]:[1.0,0.7,0]"
	States
	{
	Spawn:
		R031 PPQQRRSSTTUU 1 A_SetScale(scaleX+frandom(0.0,0.025))
		R031 VVWWXXYYZZ 1 A_FadeOut(0.07)
		Wait
	}
}
Actor DEFatsohellGN : DEfatsohell
{
	+LOOKALLAROUND
	-COUNTKILL
}

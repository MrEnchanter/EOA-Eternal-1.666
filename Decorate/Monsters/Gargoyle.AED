//================================================================================================================
//================================================================================================================
//
//	
//================================================================================================================
//================================================================================================================

ACTOR DEGargoyle : FlyAI
{
	//$Category "DE base monsters"
	//$Color 12
	Health 200
	//Health 90
	Radius 20
	Height 56
	Mass 1500 // So he is not pushed around
	Speed 20
	PainChance 192
	MeleeRange 72
	Monster
	xScale 1.1
	yScale 0.95
	MaxDropOffHeight 120
	+NEVERRESPAWN
	+FLOORCLIP
	+MissileMore
	+NoTarget
	+NoInFighting
	SeeSound 	"Gargoyle/See"
	PainSound 	"Gargoyle/Pain"
	DeathSound 	"Gargoyle/Death"
	ActiveSound "Gargoyle/Idle"
	Obituary "%o's was struck by a Gargoyle"
	DropItem "FodderHealthDrop"
	DamageFactor "PlasmaExploder", 0.3	//because otherwise, zombiemen would be killing everyone else easily
	Tag "Gargyle"
	States
	{
	Spawn:
		GARB A 0 NODELAY 
		Goto Idle
	Idle:
		GARB D 10 A_Look
		Loop
	See:
		GARB AABBCCDD 2 A_Chase
		Loop
	Melee:	
			GARB A 0 A_PlaySound ("Gargoyle/See",7,1.0,False,ATTN_NORM,0)
			GARB E 2 A_Recoil (-5)
			GARB E 2 A_FaceTarget
			GARB E 2 A_Recoil (-4)
			GARB E 2 A_FaceTarget
			GARB E 2 A_Recoil (-3)
			GARB E 2 A_FaceTarget
			GARB D 4 A_CustomMeleeAttack(2)
			TNT1 A 0 A_CustomMissile("GargoyleWaveCreator",24) //This is purely a visual effect, for the sake of feedback, due to a lack of a proper melee animation
			GARB E 2 A_FaceTarget
			GARB E 2 A_Recoil (-2)
			GARB E 2 A_FaceTarget
			GARB E 2 A_Recoil (-2)
			Goto See
	Missile:
		TNT1 A 0 A_Jump(128,"Slide")
		TNT1 A 0 A_JumpIfCloser(1024,"FireBall")	//if too far away, will always use the big ball
		
		FireBall:
			TNT1 A 0 A_Jump(96,"FireBallhuge")	//yet, if close enough, it will still use sometimes the big ball
			GARB EEEE 4 Light("ImpPrepareAttackSmall") A_FaceTarget
			GARB G 0 A_PlaySound("Gargoyle/Spit",CHAN_AUTO,1.0,false,1.3)
			GARB D 6 A_CustomMissile("DEGargoyleBall",35,5,0)
			Goto See
		FireBallhuge:
			GARB A 0 Light("ImpPrepareAttackBig") A_FaceTarget
			GARB ABCABC 2 ThrustThingZ (0,15,0,0)
			
			GARB ABCABC 2 ThrustThingZ (0,15,0,0)
			TNT1 A 0 A_SpawnItem("GargoyleheavyFX")
			GARB ABC 2 ThrustThingZ (0,15,0,0)
			TNT1 A 0 A_FaceTarget
			GARB G 0 A_PlaySound("Gargoyle/Spit",CHAN_AUTO,1.0,false,1.3)
			GARB D 6 A_CustomMissile("DEGargoyleBall",35,5,ACS_NamedExecuteWithResult("PredictAim", 1, 35, 32) * 360 / 65536 )
			TNT1 A 0 A_SpawnItem("GargoyleheavyFX")
			GARB ABC 2 ThrustThingZ (0,15,0,0)
			TNT1 A 0 A_FaceTarget
			GARB G 0 A_PlaySound("Gargoyle/Spit",CHAN_AUTO,1.0,false,1.3)
			GARB D 6 A_CustomMissile("DEGargoyleBall",35,5,ACS_NamedExecuteWithResult("PredictAim", 1, 35, 32) * 360 / 65536 )
			TNT1 A 0 A_SpawnItem("GargoyleheavyFX")
			GARB ABC 2 ThrustThingZ (0,15,0,0)
			TNT1 A 0 A_FaceTarget
			GARB G 0 A_PlaySound("Gargoyle/Spit",CHAN_AUTO,1.0,false,1.3)
			GARB D 6 A_CustomMissile("DEGargoyleBall",35,5,ACS_NamedExecuteWithResult("PredictAim", 1, 35, 32) * 360 / 65536 )
			GARB ABCDABCD 3
			Goto See
	Slide:
		TNT1 A 0 A_Jump (24,"FireBallHuge")
		TNT1 A 0 ThrustThingZ (0,30,0,0)
		TNT1 A 0 A_Jump(128,"Slide2")
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+192, 10,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	Slide2:
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+64, 10,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	SlideAfter:
		GARB E 4 A_FaceTarget
		Goto FireBall+1
	Pain:
		GARB H 2
		GARB H 2 A_Pain
		Goto See

	Death:
		"####" "#" 0  A_SpawnItemEx("SituationalPinata",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		GARB I 4 
		GARB J 4 A_Scream
		GARB K 4
		GARB L 4 A_NoBlocking
		GARB M 0
		"####" "#" 30
		TNT1 A 0 A_CustomMissile ("GrowingBloodPool", 0, 0, random (0, 360), 2, random (0, 90))
		GARB M 350 
		IMFC M 2 Bright A_Fadeout
		Wait
	
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropper",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	Raise:
		Goto See
	}
}


Actor GargoyleHeavyFX
{
	var int user_damage;
	+NORADIUSDMG
	+NOGRAVITY
	+NEVERTARGET
	-SHOOTABLE
	+NOTONAUTOMAP
	-SOLID
	+NOTELEPORT
	+QUICKTORETALIATE
	+GHOST
	-COUNTKILL
	+NOTARGETSWITCH
	Health 150
	Radius 32
	Height 32
	RenderStyle Add
	Mass 0x7FFFFFFF
	scale 0.05
	+THRUSPECIES
	//BloodType "PinkyArmorPuff"
	PainChance 256
	Translation "0:255=%[0,0,0]:[1.8,1.8,0.1]"
		
	States
	{
	Spawn:
		//setup stage, puts target (monster that spawned it) as master, and as tracer to keep resetting it
		TNT1 A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
	SpawnLooper:
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 TS 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.1)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 RQ 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.125)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 OP 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.15)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 NM 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.175)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 LK 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.2)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 JI 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.25)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 HG 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.3)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 FE 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.35)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 DC 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		TNT1 A 0 A_SetScale(0.4)
		TNT1 A 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		DB59 BA 1 BRIGHT A_Warp(AAPTR_TARGET,0,0,35,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Stop
	}
}

actor GargoyleWaveCreator : FastProjectile //This is purely a visual effect, for the sake of feedback, due to a lack of a proper melee animation
{
  height 2
  radius 16
  damage 6
  speed 24
  renderstyle None
  alpha 1
  PROJECTILE
  DamageType "fire"
  -ACTIVATEIMPACT 
  -ACTIVATEPCROSS
  +DONTSPLASH
  +NOBLOOD 
  +NOBLOODDECALS
  
  states
  {
  Spawn:
    TNT1 A 0 
  Death:
    TNT1 A 0 
    TNT1 A 0 A_Stop
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, 24)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, 18)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, 12)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, 6)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, 0)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, -6)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, -12)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, -18)
    TNT1 A 1 A_CustomMissile("GargleFireWave", 0, -24)
    stop
  }
}

actor GargleFireWave : FastProjectile
{
  height 2
  radius 20
  damage (0)
  speed 0
  renderstyle Add
  alpha 1
  DamageType "fire"
  seesound "misc/shot"
  deathsound "misc/shotx"
  -ACTIVATEIMPACT 
  -ACTIVATEPCROSS
  +DONTSPLASH
  +NOBLOOD 
  +NOBLOODDECALS
  yscale 0.25
  xscale 0.38
  Translation "0:255=%[0,0,0]:[1.8,1.8,0.1]"
  states
  {
  Spawn:
    TNT1 A 1 bright
  Death:
    TNT1 A 0 bright A_Stop
    TNT1 A 0 bright
    WAVE AAAABBBBCCCC 1 bright A_FadeOut(0.07)
    stop
  }
  
}



//*********************************************
ACTOR DEGargoyleBall : D4AbstractMissile
{
	Radius 6
	Height 8
	Speed 32
	FastSpeed 32
	Damage 3
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 1
	scale 0.2
	+FORCEXYBILLBOARD
	Decal "FireballScorch"
	Translation "0:255=%[0,0,0]:[1.8,1.8,0.1]"
	States
	{
	Spawn:
		DB58 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT Light("YellowMediumFlicker3") A_SpawnItem("GargoyleTrail")
		Loop
	Death:
		HIBI A 0 A_SetScale(0.4)
		DB59 AB 1 BRIGHT Light("YellowSmallFlicker1")	//had to separate
		DB59 CD 1 BRIGHT Light("YellowMediumFlicker3")
		DB59 EF 1 BRIGHT Light("YellowMediumFlicker3")
		DB59 GH 1 BRIGHT Light("YellowSmallFlicker1")
		DB59 IJ 1 BRIGHT Light("YellowSmallFlicker1")
		DB59 KL 1 BRIGHT Light("YellowSmallFlicker2")
		DB59 MN 1 BRIGHT Light("YellowSmallFlicker2")
		DB59 OP 1 BRIGHT Light("YellowSmallFlicker3")
		DB59 QR 1 BRIGHT Light("YellowSmallFlicker3")
		DB59 ST 1 BRIGHT Light("YellowSmallFlicker3")
		Stop
	}
}

Actor GargoyleTrail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle Translucent
	Alpha 0.5
	Scale 0.15
	States
	{
	Spawn:
		DB58 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright A_FadeOut(0.05)
		Wait
	}
}
Actor DEgargoyleGN : DEgargoyle
{
	+LOOKALLAROUND
	-COUNTKILL
}
//---------------------------------------------------------------------------------
//---------------------------------------------------------------------------------
//		Gore Nest
//	Sprites by Franco Tieppo
//---------------------------------------------------------------------------------
//---------------------------------------------------------------------------------

Actor DETGoreNest 13511
{
	Monster
	Health 50000
	Speed 0
	Height 5
	Radius 2
	MaxTargetRange 640
	+LOOKALLAROUND
	Species "GoreNestPart"
	ActiveSound "Doom4/Monsters/GoreNest/Active"
	+DONTTHRUST
	+BOSS
	+NOINFIGHTING
	+NOPAIN
	-COUNTKILL
	States
	{
	Spawn:
		DEGN A 0 NODELAY A_SpawnItemEx("GoreNestSphere",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		DEGN AAAAA 0 A_SpawnItemEx("GoreNestMonsterSpawnerSmall",random(-75,75),random(-75,75),40, 0,0,0,random(0,359) )
	Idle:
		DEGN AAAA 0 A_SpawnItemEx("GoreNestSmoke",randompick(20,-20),randompick(20,-20),0,frandom(2.5,-2.5),frandom(2.5,-2.5),0,random(1,360),SXF_NOCHECKPOSITION) 
		DEGN AAAAA 2 Light("GoreNestkLight") A_Look
		Loop
	See:
		DEGN A 0 A_JumpIfInventory("GoreNestDying",1,"DeathSequence")
		DEGN AAAA 0 A_SpawnItemEx("GoreNestSmoke",randompick(20,-20),randompick(20,-20),0,frandom(2.5,-2.5),frandom(2.5,-2.5),0,random(1,360),SXF_NOCHECKPOSITION) 
		DEGN A 5 Light("GoreNestkLight") 
		DEGN AAAAAA 1 Light("GoreNestkLight") A_Chase 
		Loop
		
	/*Missile:
		TNT1 A 0 A_Checksight("See") //if not in sight, go to main loop
		TNT1 A 0 A_Jump(218,"See")
		TNT1 A 0 A_SpawnItemEx("GoreNestMonsterSpawnerSmall",0,0,40, random(10,15),random(10,15),random(1,2),random(0,359) ) //A_CustomMissile("GoreNestMonsterSpawnerSmall",40,0,random(0,359))
		Goto See
		*/
	DeathSequence:
		DEGN B 2 A_FaceTarget
		DEGN B 3 A_SpawnItemEx("GoreNestblood",5,20,30,0,0,0,0,SXF_NOCHECKPOSITION)//Light("RedBigPoint3DLS")
		Loop
	Death:
		DEGN A 0 A_ChangeFlag("BOSS",0)
		TNT1 A 0 A_Quake(2,50,0,640)
		TNT1 A 1 A_PlaySound("Doom4/Gore/Big",CHAN_AUTO,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperBig",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Fall
		TNT1 AAAA 1 A_SpawnItemEx("GoreNestMonsterSpawnerBig",0,0,40, random(10,15),random(10,15),random(1,2),random(0,359) )
		TNT1 AAAA 3 A_SpawnItemEx("GoreNestMonsterSpawnerBig",0,0,40, random(10,15),random(10,15),random(1,2),random(0,359) )
		TNT1 A 15
		TNT1 A 3 A_SpawnItemEx("BloodGibDropper",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_GiveInventory("GoreNestDeathToken",1,AAPTR_MASTER)
		TNT1 A 10 A_BossDeath
		Stop
	}
}

Actor GoreNestDeathToken : Inventory {Inventory.MaxAmount 1}
Actor GoreNestDying : Inventory { }

Actor GoreNestSphere
{
	+DONTTHRUST
	+FORCEXYBILLBOARD
	Monster
	Health 50000
	WoundHealth 49700
	Radius 20
	Height 64
	Species "GoreNestPart"
	+BOSS
	+NOINFIGHTING
	+NOPAIN
	-COUNTKILL
	States
	{
	Spawn:
		DEGN A 0 NODELAY //ACS_NamedExecuteAlways("SetThisActorHealthTo",0,20000)
	Idle:
		DEGN CDEFGHFED 3 Bright 
		Loop
	Wound:
		DEGN A 0 ACS_NamedExecuteAlways("SetThisActorHealthTo",0,100000)
		//DEGN A 0 A_ChangeFlag("THRUACTORS",1)
		DEGN A 0 A_GiveInventory("GoreNestDying",1,AAPTR_MASTER)
		DEGN A 0 A_PlaySound("Doom4/Monsters/GoreNest/Scream",CHAN_AUTO,1.0,false,0.7)
		DEGN CDEFGHFEDC 3 Bright Light("RedBigPoint2") //A_SpawnItemEx("NewMeat",0,0,35,random(1,2)*velx+random(-7,7),random(1,2)*vely+random(-7,7),random(1,2)*velz+random(1,14),random(1,359), 200)
		DEGN CDEFGHFEDC 3 Bright Light("RedBigPoint2") //A_SpawnItemEx("NewMeat",0,0,35,random(1,2)*velx+random(-7,7),random(1,2)*vely+random(-7,7),random(1,2)*velz+random(1,14),random(1,359), 200)
		DEGN CDEFGHFEDC 3 Bright Light("RedBigPoint2") //A_SpawnItemEx("NewMeat",0,0,35,random(1,2)*velx+random(-7,7),random(1,2)*vely+random(-7,7),random(1,2)*velz+random(1,14),random(1,359), 200)
		DEGN CDEFGHFEDC 3 Bright Light("RedBigPoint2") //A_SpawnItemEx("NewMeat",0,0,35,random(1,2)*velx+random(-7,7),random(1,2)*vely+random(-7,7),random(1,2)*velz+random(1,14),random(1,359), 200)
		DEGN CDEFGH 3 Bright Light("RedBigPoint2") //A_SpawnItemEx("NewMeat",0,0,35,random(1,2)*velx+random(-7,7),random(1,2)*vely+random(-7,7),random(1,2)*velz+random(1,14),random(1,359), 200)
		DEGN C 1 Bright Light("RedBigPoint2") A_Die
		Goto Death
	Death:
		DEGN A 0 A_ChangeFlag("BOSS",0)
		DEGN C 0 A_Fall
		DEGN CCCDDDEEEFFF 1 Bright Light("RedBigPoint2") A_FadeOut(0.03)
		DEGN GGGHHH 1 Bright Light("RedBigPoint3") A_FadeOut(0.03)
		DEGN FFF 1 Bright Light("RedMediumPoint1") A_FadeOut(0.03)
		DEGN EEE 1 Bright Light("RedMediumPoint2") A_FadeOut(0.03)
		DEGN DDD 1 Bright Light("RedMediumPoint3") A_FadeOut(0.03)
		DEGN CCC 1 Bright Light("RedSmallPoint1") A_FadeOut(0.03)
		DEGN C 0 A_KillMaster
		TNT1 A 35
		Stop
	}
}

//------------------
Actor GoreNestMonsterSpawnerSmall
{
	Speed 12
	Radius 32
	Height 4
	Damage 0
	Projectile
	+THRUACTORS
	+THRUSPECIES
	+DONTSPLASH
	-NOGRAVITY
	+FORCEXYBILLBOARD
	+BOUNCEONWALLS
	+SKYEXPLODE
	+STEPMISSILE
	MaxStepHeight 32
	BounceFactor 1.0
	WallBounceFactor 1.0
	RenderStyle None
	Species "GoreNestPart"
	States
	{
	Spawn:
		BAL1 A 0 NODELAY //A_JumpIf(CallACS("GoreNestSpawnerSpaceCheck")==0,"Null")
		BAL1 A 0 A_Jump(240,2,3,4,5,6)
		BAL1 AAAAAAAAAAAAAA 1
		BAL1 A 0 A_Stop
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SummonTeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("GoreNestWeakSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor GoreNestMonsterSpawnerBig : GoreNestMonsterSpawnerSmall
{
	States
	{
	Death:
		TNT1 A 0 A_SpawnItemEx("SummonTeleportFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("GoreNestBigSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor GoreNestSmoke //code borrowed from QCDE
{
	+THRUACTORS
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	+FLOORCLIP
	+NOFRICTION
	RenderStyle Add
	Alpha 0.0
	Scale 0.4
	Gravity 0.08
	Radius 5
	Height 7
	Translation "0:255=%[0,0,0]:[0.9,0.1,0]"
	States
	{
	Spawn:
		X110 A 0 NODELAY A_Jump(128,"Flip")
		Goto Spawnn
	Flip:
		X110 A 0 A_SetScale(scalex*-1,scaley)
		Goto Spawnn
	Spawnn:
		X110 ABCDEFGHIJK 1 Bright A_FadeIn(0.09)
		X110 LMNOPQRSTUVWXY 1 Bright
	Looper:
		X110 ABCDEFGHIJKLMNOPQRSTUVWXY 1 Bright A_FadeOut(0.03)
		Loop
	
	}
}

//--------------------------------------------
// Monsters re-definition

Actor GoreNestWeakSpawner : RandomSpawner
{
	DropItem "DEZombiemanGN"
	DropItem "DEMechZombieGN"
	DropItem "DEDoomImpGN"
	DropItem "DEShieldGuyGN"
}

Actor GoreNestBigSpawner : RandomSpawner
{
	DropItem "DELostSoul" 255, 10
	DropItem "DEZombiemanGN", 255, 10
	DropItem "DEDoomImpGN" 255, 10
	DropItem "DEMechaZombieGN" 255, 10
	DropItem "DEMaykrDroneGN" 255, 10
	DropItem "DEDemonGN" 255, 8
	DropItem "DEProwlerGN" 255, 8
	DropItem "DESpectreGN" 255, 8
	DropItem "DECarcassGN" 255, 8
	DropItem "DERevenantGN" 255, 4
	DropItem "DEHellKnightGN" 255, 2
	DropItem "DEBaronOfHellGN" 255, 1
}


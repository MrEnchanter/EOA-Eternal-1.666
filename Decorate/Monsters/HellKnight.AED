//================================================================================================================
//================================================================================================================
//
//
//================================================================================================================
//================================================================================================================


ACTOR DEHellKnight : EDE_HellKnight
{
	//$Category "DE base monsters"
	//$Color 12
	var int user_leapstrength;
	Health 1500
	Radius 24
	Height 64
	Mass 1500
	Speed 18
	PainChance 32
	Monster
	+NEVERRESPAWN
	+FLOORCLIP
	+DONTHARMSPECIES
	+NoTarget
	+NoInFighting
	+MissileMore
	+MissileEvenMore
	SeeSound 	"Doom4/Monsters/HellKnight/Sight"
	ActiveSound "Doom4/Monsters/HellKnight/Idle"
	PainSound 	"Doom4/Monsters/Baron/Pain"
	DeathSound 	"Doom4/Monsters/HellKnight/Death"
	HitObituary "$OB_KNIGHTHIT"
	Obituary "$OB_KNIGHT"
	Scale 1.05
	Gravity 0.83
	WoundHealth 60
	MeleeRange 72
	Damage 3
	Tag "Hell Knight"
	DropItem "ZombieTimer", 192
	+DONTHARMSPECIES
	DropItem "HeavyMediumDrop"
	States
	{
	Spawn:
		VOS2 A 0
		Goto Idle
	Idle:
		VOS2 A random(5,15)
	IdleLoop:
		VOS2 AB 10 A_Look 
		Loop
	See:
		VOS2 AAABBBCCCDDD 2 A_Chase
		Loop
		
	SeeNoMissile:		//reached only when landing after a jump
		BOZ2 G 20 //waits a bit there, looking majestic 
		VOS2 AAABBBCCCDDD 2 A_Chase("Melee")	//then continues chasing, no jumping for a little while
		VOS2 AAABBBCCCDDD 2 A_Chase("Melee")
		VOS2 AAABBBCCCDDD 2 A_Chase("Melee")
		Goto See
	//--------------------------------------
	// Melee attack		
	Melee:
		VOS2 E 3 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 3 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 0 A_PlaySound("Doom4/Other/MeleeSwing",5,1.0,false,1.1)
		VOS2 E 3 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 G 3 A_CustomMeleeAttack(random(10,30),"Doom4/Other/MeleeHit","",0,1)
		VOS2 F 2 A_Recoil(-2)
		TNT1 A 0 A_FaceTarget
		VOS2 F 2 A_Recoil(-2)
		TNT1 A 0 A_FaceTarget
		Goto See
	Lung:
		TNT1 A 0 A_Jump(140,"Leap")
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-3)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
	LungFinish:
		VOS2 E 0 A_PlaySound("Doom4/Other/MeleeSwing",5,1.0,false,1.1)
		VOS2 E 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-2)
		VOS2 G 3 A_CustomMeleeAttack(random(10,30),"Doom4/Other/MeleeHit","",0,1)
		VOS2 F 2 A_Recoil(-2)
		TNT1 A 0 A_FaceTarget
		VOS2 F 2 A_Recoil(-2)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Jump(190,"See")
		Goto Lung+16
	//--------------------------------------
	// Ranged attacks	
	Missile:
		//BOS4 A 0 A_JumpIfCloser(160,"Lung")
		//BOS4 M 0 A_JumpIfCloser(512,"DecideToCharge")
		TNT1 A 0 A_JumpifCloser(256,"lung")
		BOS4 M 0 A_PlaySound("Doom4/Monsters/HellKnight/Melee",CHAN_AUTO,1.0,false,1.1)
		Goto Leap

	//--------------------------------------
	// Leap attack
	Leap:
		BOS3 XXXXX 1  A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(64,"Prep320")
		TNT1 A 0 A_JumpIfCloser(128,"Prep320")
		TNT1 A 0 A_JumpIfCloser(192,"Prep320")
		TNT1 A 0 A_JumpIfCloser(256,"Prep320")
		TNT1 A 0 A_JumpIfCloser(320,"Prep320")
		TNT1 A 0 A_JumpIfCloser(384,"Prep384")
		TNT1 A 0 A_JumpIfCloser(448,"Prep448")
		TNT1 A 0 A_JumpIfCloser(512,"Prep512")
		TNT1 A 0 A_JumpIfCloser(576,"Prep576")
		TNT1 A 0 A_JumpIfCloser(640,"Prep640")
		TNT1 A 0 A_JumpIfCloser(704,"Prep704")
		TNT1 A 0 A_JumpIfCloser(768,"Prep768")
		TNT1 A 0 A_JumpIfCloser(832,"Prep832")
		TNT1 A 0 A_JumpIfCloser(896,"Prep896")
		TNT1 A 0 A_JumpIfCloser(960,"Prep960")
		TNT1 A 0 A_JumpIfCloser(1024,"Prep1024")
		Goto See
		
		Prep64: 	TNT1 A 0 A_SetUserVar(user_leapstrength,16)	goto ContinueLeap
		Prep128:	TNT1 A 0 A_SetUserVar(user_leapstrength,20)	goto ContinueLeap
		Prep192:	TNT1 A 0 A_SetUserVar(user_leapstrength,24)	goto ContinueLeap
		Prep256:	TNT1 A 0 A_SetUserVar(user_leapstrength,26)	goto ContinueLeap
		Prep320:	TNT1 A 0 A_SetUserVar(user_leapstrength,28)	goto ContinueLeap
		Prep384:	TNT1 A 0 A_SetUserVar(user_leapstrength,30)	goto ContinueLeap
		Prep448:	TNT1 A 0 A_SetUserVar(user_leapstrength,33)	goto ContinueLeap
		Prep512:	TNT1 A 0 A_SetUserVar(user_leapstrength,35)	goto ContinueLeap
		Prep576:	TNT1 A 0 A_SetUserVar(user_leapstrength,37)	goto ContinueLeap
		Prep640:	TNT1 A 0 A_SetUserVar(user_leapstrength,39)	goto ContinueLeap
		Prep704:	TNT1 A 0 A_SetUserVar(user_leapstrength,41)	goto ContinueLeap
		Prep768:	TNT1 A 0 A_SetUserVar(user_leapstrength,43)	goto ContinueLeap
		Prep832:	TNT1 A 0 A_SetUserVar(user_leapstrength,45)	goto ContinueLeap
		Prep896:	TNT1 A 0 A_SetUserVar(user_leapstrength,48)	goto ContinueLeap
		Prep960:	TNT1 A 0 A_SetUserVar(user_leapstrength,50)	goto ContinueLeap
		Prep1024:	TNT1 A 0 A_SetUserVar(user_leapstrength,53)	goto ContinueLeap
		
		
		ContinueLeap:
			BOZ3 ABC 2 A_FaceTarget
			BOZ2 F 0 A_Recoil(-(user_leapstrength/1.5))
			BOZ2 F 2 ThrustThingZ(0,user_leapstrength,0,0)
			
		LeapLoop:
			BOZ2 F 0 A_CheckFloor("LeapEnd")
			BOZ2 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")	
			BOZ2 F 0 A_CheckFloor("LeapEnd")
			BOZ2 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")
			BOZ2 F 0 A_CheckFloor("LeapEnd")
			BOZ2 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")
			BOZ2 F 0 A_SpawnItemEx("KnightLeapTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
			
		DropFast:
			TNT1 A 0 A_ChangeVelocity(0,0,-18,CVF_REPLACE)
			TNT1 A 0 A_CheckFloor("LeapEnd")
			BOZ2 F 1 A_JumpIf(velx==0&&velZ==0,"LeapEnd")
			Goto DropFast+1
			
			LeapEnd:
				TNT1 A 0 A_PlaySound("Doom4/Monsters/BaronCrash",7,1.0,false,0.9)
				TNT1 A 0 A_Quake(4,20,0,512,"")
				TNT1 A 0 A_SpawnItemEx("KnightShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
				TNT1 A 0 A_JumpIfCloser(140,"DoDamage")
				Goto ResumeLeapEnd
			DoDamage:
				TNT1 A 0 A_BetaSkullAttack
				Goto ResumeLeapEnd
			ResumeLeapEnd:
				BOZ2 G 1 A_Stop
				BOZ2 G 2
				Goto SeeNoMissile  
	Pain:
		VOS2 H 2
		VOS2 H 2 A_Pain
		Goto See

	Death:
		"####" "#" 0  A_SpawnItemEx("SituationalPinata",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		E02D A 3 
		E02D B 3 A_Scream 
		E02D C 3 
		E02D D 3 A_NoBlocking
		E02D EFGH 3
		E02D I -1 
		Stop
	
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperBig",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperBigMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	}
}


Actor ParticleFire_HellKnight
{
renderstyle Add
scale 0.5
alpha 0.5
speed 2
+NOCLIP
+NOINTERACTION
+CLIENTSIDEONLY
+NONETID
+NOGRAVITY
+NOTELEPORT
+FORCEXYBILLBOARD
+THRUACTORS
Translation "0:255=%[0,0,0]:[1.8,0.6,0]"

states
	{
	Spawn:
		TNT1 A 0 NoDelay 
		TNT1 A 0 A_CheckSight("Null")
		X029 ABCDEFGHIJ 2 A_FadeOut(0.03)
		Stop
	}
}

//*********************************************
ACTOR D4KnightBall
{
	Radius 6
	Height 8
	Speed 20
	FastSpeed 25
	Damage 9
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	Alpha 1
	RenderStyle Add
	SeeSound "Doom4/Monsters/Generic/ThrowFireballLow"
	DeathSound "Doom4/Other/FireballHit2"
	scale 0.75
	Translation "0:255=%[0,0,0]:[1.8,0.6,0]"
	Decal "KnightFireballScorch"
	States
	{
	Spawn:
		HIB1 AB 2 BRIGHT Light("DarkOrangeMediumFlicker2")
		Loop
	Death:
		HIB1 CC 1 BRIGHT Light("DarkOrangeFlicker1")	//had to separate
		HIB1 DD 1 BRIGHT Light("DarkOrangeFlicker2")
		HIB1 EE 1 BRIGHT Light("DarkOrangeFlicker3")
		HIB1 FF 1 BRIGHT Light("DarkOrangeSmallFlicker1")
		HIB1 GG 1 BRIGHT Light("DarkOrangeSmallFlicker1")
		Stop
	}
}
//***********************************
Actor KnightShockwave : BaronShockwave
{
	Translation "0:255=%[0,0,0]:[1.8,0.6,0]"
}

//*************************************
Actor KnightLeapTrail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle Translucent
	Alpha 0.5
	States
	{
	Spawn:
		VOS2 E 2 A_FadeOut(0.05)
		Wait
	}
}


ACTOR HKLunge
{
	Height 16
	Radius 16
	Speed 27
	Damage (5*Random(4,8))
	Species "Fiends"
	SeeSound ""  
	DeathSound "Doom4/Other/MeleeHit"
	PROJECTILE
	States
	{
	Spawn:
		TNT1 A 4
		Stop
    }
}

ACTOR HKLunge2 : HKLunge
{
	Damage (3*Random(4,6))
}

ACTOR HKLunge3 : HKLunge
{
	Damage (1*Random(0,1))
}

Actor DEhellknightGN : DEhellknight
{
	+LOOKALLAROUND
	-COUNTKILL
}

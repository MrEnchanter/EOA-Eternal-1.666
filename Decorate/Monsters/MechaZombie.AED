//================================================================================================================
//================================================================================================================
//
//	
//================================================================================================================
//================================================================================================================
Actor DEMechaZombie : EDE_Imp
{
	//$Category "DE base monsters"
	//$Color 12
	Health 150
	//Health 160
	Radius 20
	Height 56
	Speed 8
	PainChance 128
	MeleeRange 192
	Monster
	+NEVERRESPAWN
	+FLOORCLIP
	+NODROPOFF
	+NoTarget
	+NoInFighting
	SeeSound 	"MechaZombie/Idle"
	PainSound 	"MechaZombie/Pain"
	DeathSound 	"MechaZombie/Death"
	ActiveSound "MechaZombie/See"
	AttackSound ""
	//DropItem "EOA_HeavyAssaultRifle", 96
	Obituary "%o was terminated by a Mecha Zombie"
	DamageFactor "PlasmaExploder", 0.3	//because otherwise, zombiemen would be killing everyone else easily
	Tag "Mecha Zombie"
	DropItem "FodderHealthDrop"
	States
	{
	Spawn:
		CIMP A 0 NODELAY 
		Goto Idle
	Idle:
		CIMP A 10 A_Look
		Loop
	See:
		CIMP AABBCCDD 3 A_Chase
		Loop
			
	//------------------------	
	Missile:
		TNT1 A 0
		Goto AutoFire
	//------------------------
	AutoFire:
		CIMP EF 6 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"Melee")
		CIMP G 3 Bright A_CustomMissile("MechZombieBall",31,7,Random(1,-1),0,Random(2,-2))
		CIMP F 3 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"Melee")
		CIMP G 3 Bright A_CustomMissile("MechZombieBall",31,7,Random(2,-2),0,Random(2,-2))
		CIMP F 3 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"Melee")
		CIMP G 3 Bright A_CustomMissile("MechZombieBall",31,7,Random(1,-1),0,Random(2,-2))
		CIMP F 3 A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(192,"Melee")
		CIMP G 3 Bright A_CustomMissile("MechZombieBall",31,7,Random(2,-2),0,Random(2,-2))
		TNT1 A 0 A_JumpIfCloser(192,"Melee")
		TNT1 A 0 A_Jump(192,"see")
		Goto AutoFire+1
	Melee:
		CIMP A 0 A_Jump(64,"AutoFire") // 1/2 of the times, it will still go to missile
		Goto Flamethrower
	FlameThrower:
		CIMP E 5 A_FaceTarget
		CIMP F 5 A_FaceTarget
		CIMP B 0 A_PlaySound("Doom4/Other/FlamerStart",6,1.0,false,1.2)
		Goto FireLoop
	FireLoop:
		CIMP C 0  Bright A_JumpIfCloser(290,"Fire")
		Goto EndFlameThrower
	Fire:
		FATA C 0 A_FaceTarget
		CIMP G 2 A_CustomMissile("MechFlameThrower",30,0)
		FATA C 0 A_CPosRefire
		Goto FireLoop
	EndFlameThrower:
		CIMP GFE 4
		Goto See
	Pain:
		CIMP H 3
		CIMP H 3 A_Pain
		Goto See
	Death:
		CIMP I 4 
		CIMP J 4 A_Scream
		CIMP K 4 A_NoBlocking
		CIMP L 4
		CIMP M 4 A_CustomMissile ("GrowingBloodPool", 0, 0, random (0, 360), 2, random (0, 90))
		CIMP M -1
		Wait
	
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropper",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	Raise:
		Goto See
	}
}

Actor MechZombieBall : ZombiemanPlasmaBall
{
	Scale 0.15
	Speed 15
	Damage 1
	Translation "0:255=%[0,0,0]:[3.0,0.0,0.0]"
}

Actor MechFlameThrower : D4AbstractMissile
{
	var int user_counter;
	Projectile
	Radius 5
	Height 10
	RenderStyle Add
	Scale 0.4
	Speed 24
	FastSpeed 24
	Damage (2)
	+BRIGHT
	+SLIDESONWALLS
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7,8)
		DB55 ACEGI 1 
		// If collided before this, highly visible sprites are forced
		DB55 J 0 A_SetUserVar(user_counter, 1)
		DB55 KMOQSUWY 1 A_ScaleVelocity(0.95)
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1))
		DB56 ACEGIK 1 A_ChangeVelocity(-0.8,0,0.3,CVF_RELATIVE)
		Stop
	Death:
		"####" "#" 0 A_JumpIf(user_counter < 1, "Crash")
		"####" "#" 1 A_FadeOut(0.14)
		Wait
	Crash:
		DB55 KMOQSUWY 1
		DB56 EFGHIJK 1 A_FadeOut(0.14)
		Stop
		}
}
Actor DEmechazombieGN : DEmechazombie
{
	+LOOKALLAROUND
	-COUNTKILL
}

//================================================================================================================
//================================================================================================================
//
//	Pain Elemental replaced by Cacodemon
//================================================================================================================
//================================================================================================================

//ACTOR D4PainElementalReplacer : D4Cacodemon { }

Actor DEPainElemental : FlyAI
{
	//$Category "DE base monsters"
	//$Color 12
	var int user_floaterup;
	var int user_chasecount;
	Health 2000
	Radius 31
	Height 56
	Mass 9000
	Speed 8
	xScale 1.25
	yScale 1.125
	PainChance 24
	Monster
	+MissileMore
	+FLOAT 
	+NOGRAVITY
	+NoTarget
	+NoInFighting
	SeeSound	"PainElemental/See"
	PainSound	"PainElemental/Pain"
	DeathSound	"PainElemental/Death"
	ActiveSound	"PainElemental/Idle"
	MinMissileChance 600
	Scale 1.15
	Tag "Pain Elemental"
	DropItem "ZombieTimer", 192
	DropItem "HeavyMediumDrop"
	States
	{
	Spawn:
		PAIN A 0 
		Goto Idle
	Idle:
		PAIN A 4 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory("FloaterDeaths",3,"ReSummonFloater")
		TNT1 A 0 A_JumpIfInventory("FloaterCounter",3,2)
		TNT1 A 0 A_JumpIf(user_floaterup==0,"SummonFloater")
		PAIN AABBCC 3 A_Chase
		Loop
	Slide:
		TNT1 A 0 A_PlaySound ("PainElemenetal/Dodge",7,1.0,False,ATTN_NORM)
		TNT1 A 0 A_Jump(128,"Slide2")
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+192, 18,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	Slide2:
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+64, 18,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	SlideAfter:
		PAIN AABB 3 A_FaceTarget
		PAIN A 0 A_Stop
		PAIN CC 3 A_FaceTarget
		TNT1 A 0 A_Jump(64,"see")
		Goto Missile+1
	Missile:
		TNT1 A 0 A_Jump(140,"Slide")
		PAIN D 5 A_FaceTarget
		PAIN E 5 A_FaceTarget
		PAIN F 1 BRIGHT A_FaceTarget
		TNT1 A 0 A_Playsound ("PainElemenetal/Spit",7,1.0,False,ATTN_NORM)
		PAIN F 4 BRIGHT A_SpawnProjectile("DELostSoulProjectile",12)
		PAIN A 0 A_Jump(160,"see")
		PAIN D 5 A_FaceTarget
		PAIN E 5 A_FaceTarget
		PAIN F 1 BRIGHT A_FaceTarget
		TNT1 A 0 A_Playsound ("PainElemenetal/Spit",7,1.0,False,ATTN_NORM)
		PAIN F 4 BRIGHT A_CustomMissile("D4LostSoulProjectile",12,0,ACS_NamedExecuteWithResult("PredictAim", 1, 40, 32) * 360 / 65536 )
		Goto See
		
	ReSummonFloater:
		PAIN A 0 A_TakeInventory("FloaterDeaths",3)
		PAIN A 0 A_TakeInventory("FloaterCounter",3)
		PAIN A 0 A_SetUserVar(user_floaterup,0)
		PAIN A 0 A_SetUserVar(user_chasecount,3)
		Goto ChaseNoFloater
		
	ChaseNoFloater:
		PAIN AABBCC 3 A_Chase
		PAIN A 0 A_JumpIf(user_chasecount==0,"See")
		PAIN AABBCC 3 A_Chase
		PAIN A 0 A_SetUserVar(user_chasecount,user_chasecount-1)
		Loop
		
	SummonFloater:
		PAIN D 5 
		PAIN E 5 
		PAIN F 1 BRIGHT 
		TNT1 A 0 A_JumpIfInventory("FloaterCounter",2,6)
		TNT1 A 0 A_JumpIfInventory("FloaterCounter",1,3)
		PAIN F 4 BRIGHT A_SpawnItemEx("D4LostSoulFloat",0,0,12, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_Jump(256,4)
		PAIN F 4 BRIGHT A_SpawnItemEx("D4LostSoulFloat2",0,0,12, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_Jump(256,2)
		PAIN F 4 BRIGHT A_SpawnItemEx("D4LostSoulFloat3",0,0,12, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		PAIN F 0 A_JumpIfInventory("FloaterCounter",3,2)
		PAIN F 0 A_Jump(256,2)
		PAIN F 0 A_SetUserVar(user_floaterup,1)
		PAIN F 0
		Goto See
		
	FireSkullMissile:
		PAIN D 5 A_FaceTarget
		PAIN E 5 A_FaceTarget
		PAIN F 1 BRIGHT A_FaceTarget
		TNT1 A 0 A_Playsound ("PainElemenetal/Spit",7,1.0,False,ATTN_NORM)
		PAIN FFF 1 BRIGHT A_SpawnProjectile("D4LostSoulProjectile",12)
		PAIN F 0 A_JumpIf(user_chasecount>0,"ChaseNoFloater")
		Goto See
		
	Pain:
		PAIN G 6
		PAIN G 6 A_Pain
		PAIN G 0 A_JumpIf(user_chasecount>0,"ChaseNoFloater")
		Goto See
	Death:
		"####" "#" 0 A_ChangeFlag(THRUACTORS,true)
		"####" "#" 0 A_KillChildren
		PAIN H 0 A_Stop
		PAIN H 1 BRIGHT A_Scream
		PAIN HHH 1 BRIGHT A_SpawnItemEx("PainElementalExplosionSprite",0,0,28,frandom(-3,3),frandom(-3,3),frandom(-3,3),random(0,359),SXF_NOCHECKPOSITION)
		Goto XDeath
	
	Death.Massacre:
		"####" "#" 0 A_KillChildren
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		"####" "#" 0 A_KillChildren
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 AA 0 A_SpawnItemEx("BloodGibDropperBig",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 AA 0 A_SpawnItemEx("BloodGibDropperBigMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	
	}
}

Actor PainElementalExplosionSprite
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Alpha 0.5
	Scale 0.7
	RenderStyle Add
	Alpha 0.99
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(frandom(0.8,1.1))
		PROJ DEFGHI 3 Bright
		Stop
	}
}


ACTOR D4LostSoulProjectile // "fast ball" projectile
{
	Health 15
	Radius 8
	Height 8
	Speed 26
	FastSpeed 26
	Damage 0
	Projectile
	+NEVERRESPAWN
	-NOBLOCKMAP
	+NOTARGETSWITCH
	+DONTFALL
	+NOBLOOD
	+NOICEDEATH
	+SHOOTABLE
//	+SEEKERMISSILE
	SeeSound	"Doom4/Monsters/LostSoul/Attack"
	DeathSound	"skull/death"
	Obituary "$OB_SKULL"
	Tag "Lost Soul"
	
	States
	{
	Spawn:
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT Light("BlueSmallFlicker1DLS")
	Looper:
		PSUL B 0 A_SpawnItemEx("LostSoulRedFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL B 1 A_SpawnItemEx("LostSoulRedFlare",-5,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		Loop
	
	XDeath:
	Death:
		PSUL A 0 A_CustomMissile("LostSoulExploder",0,0,0)
		TNT1 A 0 A_Explode(32,32)
		PSUL DDDDD 0 A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("BlueMediumFlicker1")
		TNT1 A 2 Light("BlueMediumFlicker2")
		TNT1 A 1 Light("BlueMediumFlicker3")
		TNT1 A 1 Light("BlueSmallFlicker1")
		TNT1 A 1 Light("BlueSmallFlicker2")
		TNT1 A 1 Light("BlueSmallFlicker3")
		Stop
	}
}

Actor DELostSoulProjectile
{
	Projectile
	Speed 24
	FastSpeed 24
	Damage (8)
	Radius 3
	Height 4
	+SeekerMissile
	SeeSound	"Doom4/Monsters/LostSoul/Attack"
	DeathSound	"skull/death"
	Decal "D4ExploScorchSmallest"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(220,4,8,12,16,20,22,26)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Weave(1,1,1.0,1.0)	//so that it starts at a random offset of the weave
		Goto Idle
	Idle:
		PSUL AA 1 Bright Light("BlueSmallPoint1") A_Weave(Random(2,3),Random(2,3),1.0,1.0)
		PSUL AA 0 Bright Light("BlueSmallPoint1") A_SeekerMissile(1,1)
		PSUL AAA 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		Loop
	Death:
		PSUL A 0 A_CustomMissile("LostSoulExploder",0,0,0)
		PSUL DDDDD 0 A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("BlueMediumFlicker1")
		TNT1 A 2 Light("BlueMediumFlicker2")
		TNT1 A 1 Light("BlueMediumFlicker3")
		TNT1 A 1 Light("BlueSmallFlicker1")
		TNT1 A 1 Light("BlueSmallFlicker2")
		TNT1 A 1 Light("BlueSmallFlicker3")
		Stop
	}
}

ACTOR D4LostSoulFloat
{
	Health 80
	Radius 14
	Height 42
	Speed 12
	Scale 0.75
	-NOBLOCKMAP
	+NOBLOOD
	+NOICEDEATH
	+NOGRAVITY
	+NEVERTARGET
	+SHOOTABLE
	+NOTONAUTOMAP
	-SOLID
	+QUICKTORETALIATE
	+GHOST
	-COUNTKILL
	+NOTARGETSWITCH
	+THRUSPECIES
	+DONTTHRUST
	+FLOATBOB
	FloatBobStrength 0.3
	//SeeSound	"Doom4/Monsters/LostSoul/Attack"
	DeathSound	"skull/death"
	Obituary "$OB_SKULL"
	Tag "Lost Soul"
	PainChance 38
	Species "D4LostSoul"
		
	
	States
	{
	Spawn:
		PSUL A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_DEFAULT)
		PSUL A 0 A_GiveInventory("FloaterCounter",1,AAPTR_MASTER)
	FloatLooper:	
		PSUL A 0 A_JumpIfCloser(256,"DecideToLaunch")
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,0,66,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT 
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,0,66,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT 
		Loop
		
	Pain:
		PSUL A 2 A_Facetarget
		TNT1 A 1 A_SpawnProjectile("D4LostSoulProjectile")
		Goto Death
	
	DecideToLaunch:
		PSUL A 0 A_Jump(12,"LaunchProjectile")
		Goto FloatLooper+1
		
	LaunchProjectile:
		PSUL A 2 A_Facetarget
		TNT1 A 1 A_SpawnProjectile("D4LostSoulProjectile")
		Goto Death
		
	XDeath:
	Death:
		TNT1 A 1 A_GiveInventory("FloaterDeaths",1,AAPTR_MASTER)
		PSUL DDDDD 0 A_SpawnItemEx("LostSoulExploParticle",0,0,25,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 1 Light("BlueMediumFlicker1")
		TNT1 A 1 Light("BlueMediumFlicker2")
		TNT1 A 1 Light("BlueMediumFlicker3")
		TNT1 A 1 Light("BlueSmallFlicker1")
		TNT1 A 1 Light("BlueSmallFlicker2")
		TNT1 A 1 Light("BlueSmallFlicker3")
		Stop
	}
}

ACTOR D4LostSoulFloat2 : D4LostSoulFloat
{
	States
	{
	Spawn:
		PSUL A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_DEFAULT)
		PSUL A 0 A_GiveInventory("FloaterCounter",1,AAPTR_MASTER)
	FloatLooper:	
		PSUL A 0 A_JumpIfCloser(256,"DecideToLaunch")
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,27,62,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT 
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,27,62,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT  
		Loop
	DecideToLaunch:
		PSUL A 0 A_Jump(12,"LaunchProjectile")
		Goto FloatLooper+1	
	}
}	
ACTOR D4LostSoulFloat3 : D4LostSoulFloat
{
	States
	{
	Spawn:
		PSUL A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_DEFAULT)
		PSUL A 0 A_GiveInventory("FloaterCounter",1,AAPTR_MASTER)
	FloatLooper:	
		PSUL A 0 A_JumpIfCloser(256,"DecideToLaunch")
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,-27,62,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT 
		PSUL A 3 BRIGHT A_Warp(AAPTR_MASTER,0,-27,62,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlame",random(-5,-15),random(10,-10),25,frandom(0,-1.5),frandom(1.5,-1.5),frandom(1,2),0,SXF_NOCHECKPOSITION,160)
		PSUL A 0 A_SpawnItemEx("LostSoulBlueFlare",-8,random(10,-10),25+random(10,-10),0,0,frandom(0.1,0.5),0,SXF_NOCHECKPOSITION,128)
		PSUL A 3 BRIGHT 
		Loop
	DecideToLaunch:
		PSUL A 0 A_Jump(12,"LaunchProjectile")
		Goto FloatLooper+1	
	}
}	
		
Actor FloaterCounter : Inventory { Inventory.MaxAmount 3 }
Actor FloaterDeaths : Inventory { Inventory.MaxAmount 3 }

Actor DEpainelementalGN : DEpainelemental
{
	+LOOKALLAROUND
	-COUNTKILL
}
//================================================================================================================
//================================================================================================================
//
//
//================================================================================================================
//================================================================================================================
ACTOR DERevenant : EDE_Revenant
{
	//$Category "DE base monsters"
	//$Color 12
	var int user_counter;
	Health 750
	//Health 640
	Radius 20
	Height 56
	Mass 3500 //sorry, had to, to avoid pushing it easily when jetpacking
	Speed 13
	PainChance 70
	MeleeRange 72
	Monster
	//MeleeThreshold 196
	+NEVERRESPAWN
	+FLOORCLIP
	+DONTCORPSE
	+BLOODLESSIMPACT
	+NoTarget
	+NoInFighting
	Species "Skeletals"
	SeeSound	"Doom4/Monsters/Revenant/Sight"
	PainSound	"Doom4/Monsters/Revenant/Pain"
	DeathSound	"Doom4/Monsters/Revenant/Death"
	ActiveSound	"Doom4/Monsters/Revenant/Idle"
	MeleeSound "skeleton/melee"
	HitObituary "$OB_UNDEADHIT"
	Obituary "$OB_UNDEAD"
	Scale 0.9
	Tag "Revenant"
	DropItem "ZombieTimer", 192
	DropItem "HeavyMediumDrop"
	States
	{
	Spawn:
		SKEL A 0 NODELAY 
		Goto Idle
	Idle:
		SKEL A 0 A_ChangeFlag("Nogravity",false)
		SKEL A 0 A_ChangeFlag("FLOATBOB", false)
		SKEL A 0 A_ChangeFlag("THRUSPECIES",false)
		SKEL A 0 A_RemoveChildren(true,RMVF_EVERYTHING)
	IdleLoop:	
		SKEL AB 10 A_Look
		Loop
	See:
		SKEL AABBCCDDEEFF 2 A_Chase
		Loop
	Melee:
		SKEL G 0 A_PlaySound("Doom4/Other/MeleeSwing",CHAN_BODY,0.7,false,1.2)
		SKEL G 3 A_Recoil(-4)
		SKEL G 0 A_FaceTarget
		SKEL G 3 A_Recoil(-3)
		SKEL G 0 A_FaceTarget
		SKEL H 3 A_Recoil(-2)
		SKEL H 0 A_FaceTarget
		SKEL H 3 A_Recoil(-2)
		SKEL H 0 A_FaceTarget
		SKEL I 6 A_CustomMeleeAttack(2*random(1,4),"Doom4/Other/MeleeHit")
		Goto See
	Missile:
		TNT1 A 0 A_Jump(64,"JetPack")
		TNT1 A 0 A_Jump (192,"Slide")
		Goto Barrage
	Slide:
		TNT1 A 0 A_Jump (24,"Barrage")
		TNT1 A 0 ThrustThingZ (0,35,0,0)
		TNT1 A 0 A_Jump(128,"Slide2")
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+192, 7,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	Slide2:
		TNT1 A 0 A_Jump(128,"Slide3")
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+64, 7,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	Slide3:
		TNT1 A 0 A_Jump(128,"Slide4")
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+192, 12,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	Slide4:
		TNT1 E 0 A_FaceTarget
		TNT1 E 0 ThrustThing (angle*256/360+64, 12,0,0)
		TNT1 E 0 A_FaceTarget 
		Goto SlideAfter
	SlideAfter:
		GARB A 0 A_FaceTarget
		TNT1 A 0 A_Jump(256,"Barrage","Barragelong","BarrageShort")
		Goto Barrage
	JetPack:
		SKEL J 0 A_SetUserVar(user_counter,0)
		SKEL J 0 A_FaceTarget
		SKEL J 0 A_PlaySound("Doom4/Monsters/Revenant/Attack",CHAN_AUTO,1.0,false,1.1)
		SKEL J 0 A_PlaySound("Player/Demon/Revenant/JetPackStartLoop",6,1.0,false,1.2)
		//SKEL J 0 A_SpawnItemEx("RevenantPlaceHolder", 0,0,0, 0,0,0,0, SXF_NOCHECKPOSITION|SXF_ISTRACER)
		SKEL J 0 A_ChangeFlag("FLOATBOB", true)
		SKEL J 0 A_ChangeFlag("Nogravity",true)
		SKEL J 0 A_ChangeFlag("THRUSPECIES",true)
	JetPackFlyLoop:
		SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
		SKEL JJJ 1 Light("OrangeMediumFlicker1") A_Warp(AAPTR_DEFAULT, 0,0,5)
		SKEL J 0 A_Jump(200, "JetPackFlyLoop")
		Goto JetPackAimLoop
		
		JetPackAimLoop:
			TNT1 A 0 A_Stop
			SKEl J 1 A_SpawnItemEx("RevenantFlairPuff",12,14,50)
			SKEL J 1 A_SpawnItemEx("RevenantFlairPuff",12,-14,50)
			SKEL J 0 A_JumpIf(user_counter>=15,"FireRocketsFromJetpack")
			SKEL J 0 A_SetUserVar(user_counter,user_counter+1)
			SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
			SKEL J 1 Light("OrangeMediumFlicker1") A_FaceTarget
		JetpackAimlooploop:
			SKEL J 0
			Goto JetpackAimLoop+3
			
			FireRocketsFromJetpack:
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,-4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTarget
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTarget
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,-4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTarget
				
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,-4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTargeT
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,-4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTarget
				SKEL J 5 A_CustomMissile("RevenantRocketSeeking",50,4,0)
				SKEL J 0 A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
				SKEL J 0 Light("OrangeMediumFlicker1") A_FaceTarget
				SKEL J 0 A_PlaySound("Player/Demon/Revenant/JetPackEndThrust",6,1.0,false,1.2)
				
				SKEL J 0 A_ChangeFlag("Nogravity",false)
				SKEL J 0 A_ChangeFlag("FLOATBOB", false)
				SKEL K 10
				SKEL J 0 A_ChangeFlag("THRUSPECIES", false)
				SKEL J 0 A_RemoveChildren(true,RMVF_EVERYTHING)
				SKEL K 10
				Goto See
		
		
	Barrage:
		SKEL KK 5 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,-14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,-14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,-14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,14,0)
		SKEL J 0 A_FaceTarget
		SKEL J 7 Bright A_CustomMissile("RevenantRocketSpinning",50,-14,0)
		SKEL K 10 A_FaceTarget
		Goto See

	Pain:
		//SKEL L 0 A_RemoveChildren(true,0,"RevenantPlaceHolder","Skeletals")
		SKEL L 0 A_Remove(AAPTR_Tracer,RMVF_MISC,"RevenantPlaceHolder","Skeletals")
		SKEL L 0 A_ChangeFlag("Nogravity",false)
		SKEL L 0 A_ChangeFlag("FLOATBOB", false)
		SKEL L 0 A_ChangeFlag("THRUSPECIES",false)
		SKEL L 5
		SKEL L 5 A_Pain
		Goto See
	Death:
		"####" "#" 0 A_ChangeFlag("FLOATBOB", false)
		"####" "#" 0 A_ChangeFlag("THRUSPECIES", false)
		"####" "#" 0 A_ChangeFlag("Nogravity",false)
		"####" "#" 0 A_Remove(AAPTR_Tracer,RMVF_MISC,"RevenantPlaceHolder","Skeletals") //A_RemoveChildren(true,RMVF_EVERYTHING)
		SKEL LM 5 
		SKEL N 5 A_Scream
		SKEL O 5 A_NoBlocking
		SKEL P 5
		SKEL Q 0
		"####" "#" 30
		TNT1 A 0 A_CustomMissile ("GrowingBloodPool", 0, 0, random (0, 360), 2, random (0, 90))
		SKEL Q 0 A_ChangeFlag(CORPSE,true)
		SKEL Q 350
		RVFC Q 2 Bright A_Fadeout
		Wait
	
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperBig",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperBigMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	
	}
}

Actor RevenantFlairPuff
{
	-Shootable
	-Solid
	+NoGravity
	+ForceXYBillboard
	Renderstyle add
	Alpha 0.99
	Scale 0.05
	Translation "0:255=%[0,0,0]:[1.8,0.4,0.1]"
	States
	{
	Spawn:
		DB50 F 1 BRIGHT A_SetScale(0.4,0.4,0,0)
		DB50 G 1 BRIGHT A_SetScale(0.8,0.8,0,0)
		DB50 H 1 BRIGHT A_SetScale(1.2,1.2,0,0)
		DB50 I 1 BRIGHT A_SetScale(1.6,1.6,0,0)
		DB50 J 1 BRIGHT A_SetScale(1.4,1.4,0,0)
		DB50 K 2 BRIGHT A_SetScale(1.2,1.2,0,0)
		DB50 L 2 BRIGHT A_SetScale(1.0,1.0,0,0)
		DB50 M 2 BRIGHT A_SetScale(0.8,0.8,0,0)
		DB50 N 2 BRIGHT A_SetScale(0.6,0.6,0,0)
		DB50 M 2 BRIGHT A_SetScale(0.4,0.4,0,0)
		DB50 N 2 BRIGHT A_SetScale(0.2,0.2,0,0)
		DB50 M 2 BRIGHT A_SetScale(0.05,0.05,0,0)
		Stop
	}
}



Actor RevenantJetpackTrailSpawner
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("RevenantJetPackTrail", -15,0,32,  0,0,-2, 0, SXF_NOCHECKPOSITION,64)
		Stop
	}
}
Actor RevenantJetPackTrail //centered flare behind the rocket
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.7
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(255,"One","Two")
	One:
		X007 ABCDE 1 Bright 
		X007 FGHIJ 1 Bright
		Stop
	Two:
		X007 KLMNO 1 Bright
		X007 PQRST 1 Bright
		Stop		
	}
}
//-----------------------
Actor RevenantRocketSpinning : D4AbstractMissile
{
	Projectile
	Speed 24
	FastSpeed 30
	Damage (4)
	Radius 3
	Height 6
	Scale 0.25
	SeeSound "Doom4/Weapon/Machinegun/MiniMissileFireLegacy"
	Decal "D4ExploScorchSmallest"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(220,4,8,12,16,20,22,26)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Weave(2,2,1.0,1.0)	//so that it starts at a random offset of the weave
		Goto Idle
	Idle:
		NAIL AAAA 1 Bright Light("OrangeSmallPoint1") A_Weave(2,2,1.0,1.0)
		NAIL AA 0 A_SpawnItemEX("RevRocketTrail",Random(1,-1),Random(1,-1),Random(1,-1))
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Doom4/Weapon/Machinegun/MiniMissileExplo",CHAN_AUTO,1.0,false,1.05)
		TNT1 A 0 A_SpawnItemEx("MGMiniMissileExplo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("OrangeMediumPoint2")
		TNT1 A 2 Light("OrangeMediumPoint3")
		TNT1 A 2 Light("OrangeSmallPoint1")
		TNT1 A 2 Light("OrangeSmallPoint2")
		TNT1 A 2 Light("OrangeSmallPoint3")
		Stop
	}
}
Actor RevRocketTrail
{
	+NoGravity
	RenderStyle Add
	Scale 0.16
	Translation "0:255=%[0,0,0]:[0.3,0.3,0.3]"
	States
	{
	Spawn:
	DB59 ABCDEFGHIJKLMNOPQRST 1
	Stop
	}
}
//-----------------------
Actor RevenantRocketSeeking: D4AbstractMissile
{
	Projectile
	Speed 12
	FastSpeed 12
	Damage (5)
	Radius 3
	Height 6
	Scale 0.3
	+SeekerMissile
	Translation "0:255=%[0,0,0]:[1.8,0.4,0.1]"
	SeeSound "Doom4/Weapon/Machinegun/MiniMissileFireLegacy"
	Decal "D4ExploScorchSmallest"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(220,4,8,12,16,20,22,26)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Weave(Random(1,4),Random(1,4),1.0,1.0)	//so that it starts at a random offset of the weave
		Goto Idle
	Idle:
		NAIL AA 1 Bright Light("OrangeSmallPoint1") A_Weave(3,3,1.0,1.0)
		NAIL AA 0 Bright Light("OrangeSmallPoint1") A_SeekerMissile(1,1)
		NAIL AAA 0 A_SpawnItemEX("RevRocketSeekingTrail",Random(1,-1),Random(1,-1),Random(1,-1))
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Doom4/Weapon/Machinegun/MiniMissileExplo",CHAN_AUTO,1.0,false,1.05)
		TNT1 A 0 A_SpawnItemEx("MGMiniMissileExplo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("OrangeMediumPoint2")
		TNT1 A 2 Light("OrangeMediumPoint3")
		TNT1 A 2 Light("OrangeSmallPoint1")
		TNT1 A 2 Light("OrangeSmallPoint2")
		TNT1 A 2 Light("OrangeSmallPoint3")
		Stop
	}
}
Actor RevRocketSeekingTrail
{
	+NoGravity
	RenderStyle Add
	Scale 0.12
	Translation "0:255=%[0,0,0]:[0.6,0.3,0.1]"
	States
	{
	Spawn:
	DB59 ABCDEFGHIJKLMNOPQRST 1 BRIGHT
	Stop
	}
}

Actor MonsterRocketTrailFire //centered flare behind the rocket
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.7
	Scale 0.11
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(255,"One","Two")
	One:
		X007 ABCDE 1 Bright //A_Warp(AAPTR_MASTER,30,-20,50,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		X007 FGHIJ 1 Bright //A_Warp(AAPTR_MASTER,30,-20,50,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Stop
	Two:
		X007 KLMNO 1 Bright //A_Warp(AAPTR_MASTER,30,-20,50,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		X007 PQRST 1 Bright //A_Warp(AAPTR_MASTER,30,-20,50,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Stop		
	}
}

Actor RocketGasTrail : MonsterRocketTrailFire
{
	Scale 0.1
	Alpha 0.8
	States
	{
	Spawn:
		X102 V 2 A_FadeOut(0.08)
		Loop
	}
}

Actor DErevenantGN : DErevenant
{
	+LOOKALLAROUND
	-COUNTKILL
}
//================================================================================================================
//================================================================================================================
//
//	
//================================================================================================================
//================================================================================================================
ACTOR DEShieldGuy : EDE_Imp
{
	//$Category "DE base monsters"
	//$Color 12
	var int user_shieldup;
	var int user_ballsfired;
	Health 350
	Radius 20
	Height 56
	Mass 666
	Speed 12
	PainChance 128
	Monster
	+NEVERRESPAWN
	+FLOORCLIP
	+NOTARGET
	+NoTarget
	+NoInFighting
	Translation "112:127=176:191"
	SeeSound 	"Doom4/Monsters/Grunt/Sight"
	PainSound 	"Doom4/Monsters/Grunt/Pain"
	DeathSound 	"Doom4/Monsters/Grunt/Death"
	ActiveSound "Doom4/Monsters/Grunt/Idle"
	Obituary "$OB_SHOTGUY"
	DamageFactor "ShieldExplode", 30
	DamageFactor "PlasmaExploder", 0.3	//because otherwise, zombiemen would be killing everyone else easily
	WoundHealth 15
	Scale 1.2
	Tag "Shield Soldier"
	DropItem "FodderHealthDrop"
	MeleeThreshold 300
	MeleeRange 160
	MaxTargetRange 500
	States
	{
	Spawn:
		RPZM A 0
		Goto Idle
	Idle:
		RPZM A random(5,15)
	IdleLoop:
		RPZM A 10 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIfInventory("CounterToShieldAgain",10,"PrepareToReshield")
		TNT1 A 0 A_JumpIf(user_shieldup==0,"SummonShield")
		RPZM AABBCCDD 2 A_Chase
		Loop
		
	PrepareToReshield:
		RPZM A 0 A_TakeInventory("CounterToShieldAgain",32767)
		RPZM A 0 A_SetUserVar(user_shieldup,0)
		Goto See
		
	SummonShield:
		RPZM E 20
		RPZM E 0 A_SetUserVar(user_shieldup,1)
		RPZM A 0 A_SpawnItemEx("ShieldguyShield",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
		Goto See

	//-------------------------------------------------
	// Attacks
	Melee:
		//RPZM AABBC 3 A_Chase("","") //melee? yeah, he still walks towards you >:D
		RPZM E 0 A_FaceTarget
		RPZM E 0 A_PlaySound("Doom4/Monsters/HellShotgun",CHAN_WEAPON,1.0,false,0.9)
		RPZM FFFFFFFF 0 A_CustomMissile("ShieldGuyTracer",28,5,random(20,-20),0)
		RPZM G 5 Bright
		RPZM E 8 A_FaceTarget
		RPZM E 0 A_FaceTarget
		RPZM E 0 A_PlaySound("Doom4/Monsters/HellShotgun",CHAN_WEAPON,1.0,false,0.9)
		RPZM FFFFFFFF 0 A_CustomMissile("ShieldGuyTracer",28,5,random(20,-20),0)
		RPZM G 5 Bright
		RPZM E 8 A_FaceTarget
		RPZM E 0 A_FaceTarget
		RPZM E 0 A_PlaySound("Doom4/Monsters/HellShotgun",CHAN_WEAPON,1.0,false,0.9)
		RPZM FFFFFFFF 0 A_CustomMissile("ShieldGuyTracer",28,5,random(10,-20),0)
		RPZM G 5 Bright
		RPZM E 50	//a fair chance to hit him in the back
		Goto See
		
		//------------------------
	Missile:
		Goto See

	Pain:
		RPZM I 3
		RPZM I 3 A_Pain
		Goto See
	Death:
		"####" "#" 0  A_SpawnItemEx("SituationalPinata",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_KillChildren		//removes shield
		TNT1 A 0 A_GiveInventory("ImDead",1)
		RPZM J 3 
		RPZM K 3 A_Scream
		RPZM L 3 A_NoBlocking
		RPZM M 3
		RPZM N -1
		Stop
	
	Death.Massacre:
		TNT1 A 0 A_KillChildren
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Doom4/Gore/Minimal",CHAN_AUTO,1.0,false,2.0)
		TNT1 A 35 A_BossDeath
		Stop
	
	XDeath:
		TNT1 A 0 A_KillChildren
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("enhanced_gibs") == false,"XDeathMinimal")
		TNT1 A 0 A_Jump(128,"BologneseXDeath")
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropper",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_Jump(256,3)
	XDeathMinimal:
		TNT1 A 0 A_PlaySound("Doom4/Gore/Medium",7,1.0,false,1.3)
		TNT1 A 0 A_SpawnItemEx("BloodGibDropperMinimal",0,0,28,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 35 A_BossDeath
		Stop	
		
	Raise:
		Goto See
	}
}
Actor ShieldguyShield
{
	var int user_damage;
	var int user_colorassigned;
	+NOGRAVITY
	+NEVERTARGET
	+SHOOTABLE
	+NOTONAUTOMAP
	-SOLID
	+QUICKTORETALIATE
	+GHOST
	-COUNTKILL
	+NOTARGETSWITCH
	Health 350
	Radius 18
	Height 56
	RenderStyle Add
	Scale 0.9
	Mass 0x7FFFFFFF
	DamageFactor "Plasma", 3
	BloodType "ShieldPuff"
	PainChance 255
	States
	{
	Spawn:
		//setup stage, puts target (monster that spawned it) as master, and as tracer to keep resetting it
		Q05S A 0 NODELAY A_RearrangePointers(AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
	Looper:
		TNT1 "#" 0 A_JumpIfHealthLower(100,"Red")
		TNT1 "#" 0 A_JumpIfHealthLower(300,"Purple")
	Looper2:
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Q05S "#" 0 A_JumpIfInTargetInventory("ImDead",1,"Death.Removed") //failsafe to remove this shield
		//repeat to perform the "if health lower" less often
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Q05S "#" 0 A_JumpIfInTargetInventory("ImDead",1,"Death.Removed") //failsafe to remove this shield
		Loop
	
		Purple:
			Q05S G 0 
			Goto Looper2		
		Red:
			Q05S H 0 
			Goto Looper2
		
	Pain:
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 0 A_PlaySound("Doom4/Monsters/ZomShieldImpact",7,0.5,false,1.2)
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Q05S "#" 0 A_RearrangePointers(AAPTR_TRACER,AAPTR_TARGET,AAPTR_DEFAULT) 
		Q05S "#" 1 BRIGHT A_Warp(AAPTR_TARGET,10,-5,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Goto Looper
	Death:	//when the shield was destroyed due to damage
		TNT1 A 3 
		//Every half second it will give one counter, after 10 counters, the monster will try to resummon shield again
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 17
		TNT1 A 0 A_GiveInventory("CounterToShieldAgain",1,AAPTR_MASTER)
		TNT1 A 5
		Stop
	Death.Plasma:
		TNT1 A 0 A_SpawnItemEX("ShieldExplotionFX",0,0,48,0,0,0,0,SXF_NOCHECKPOSITION,0,0)
	Death.Removed:	//when it was deactivated by stun, stagger, or whatever else
		TNT1 A 10 A_PlaySound("Doom4/Monsters/ZombieShieldFail",CHAN_AUTO,1.0,false,1.2)
		Stop
	}
}
Actor ShieldPuff
{
	+NOINTERACTION
	RenderStyle Add
	Scale 0.4
	States
	{
	Spawn:
		Q05S B 1 Bright A_Stop
		Q05S C 1 Bright A_Stop
		Q05S D 1 Bright 
		Q05S E 1 Bright 
		Q05S F 1 Bright A_FadeOut(0.2)
		Wait
	}
}

Actor CounterToShieldAgain : Inventory { Inventory.MaxAmount 10 }



Actor ShieldGuyTracer
{
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOEXTREMEDEATH
	+THRUGHOST
	+BLOODSPLATTER
	damage (random(1,2))
	radius 2
	height 2
	speed 18
	renderstyle ADD
	alpha 0.9
	scale 0.06
	Translation "0:255=%[0,0,0]:[1.5,0.3,0.08]"
	states
	{
	Spawn:
		TRAC A 1 NODELAY ThrustThingZ(0,random(12,-12),0,1)
		TRAC A 10 BRIGHT A_PlaySound("Doom4/Other/BulletWhoosh",5,1.0,false,1.3)
		Stop
	Death:
		TNT1 A 1
		Stop
	XDeath:
		TNT1 A 1
		Stop
	}
}

Actor ShieldExplotionFX
{
	Projectile
	+NOINTERACTION
	+BRIGHT
	+NORADIUSDMG
	+NOGRAVITY
	+NEVERTARGET
	+SHOOTABLE
	+NOTONAUTOMAP
	-SOLID
	+NOTELEPORT
	+QUICKTORETALIATE
	+GHOST
	-COUNTKILL
	+NOTARGETSWITCH
	+DontHurtSpecies
	Scale 0.05
	Obituary "$OB_BARREL" // "%o went boom."
	DamageType "Plasma"
	Species "Players"
	RenderStyle Add
	Alpha 0.9
	Translation "0:255=%[0,0,0]:[0.4,0.8,1.2]"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(128,"Type2")
	Type1:
		TNT1 A 0 A_Explode(160,160,XF_NOTMISSILE|XF_HURTSOURCE,0,0,0,0,0,"Plasma")
		X072 L 1 Bright NODELAY A_SetScale(scalex+0.09)
		TNT1 A 0 A_FadeOut(0.07)
		Goto Type1+1

	Type2:
		TNT1 A 0 A_Explode(160,160,XF_NOTMISSILE|XF_HURTSOURCE,0,0,0,0,0,"Plasma")
		X072 L 1 Bright NODELAY A_SetScale(scalex+0.09)
		TNT1 A 0 A_FadeOut(0.07)
		Goto Type2+1
	}
}


Actor DEshieldguyGN : DEshieldguy
{
	+LOOKALLAROUND
	-COUNTKILL
}
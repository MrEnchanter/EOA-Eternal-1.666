//================================================================================================================
//================================================================================================================
//
//
//================================================================================================================
//================================================================================================================


ACTOR DETyrant : EDE_Boss
{
	//$Category "DE base monsters"
	//$Color 12
	var int user_rocketcount;
	var int user_laserShot;
	Health 9001
	Radius 40
	Height 128
	Scale 1.1
	Mass 9000
	Speed 13
	PainChance 6
	Monster
	MinMissileChance 500
	+NEVERRESPAWN
	+MISSILEMORE
	+MISSILEEVENMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+NOFEAR
	+DONTHARMSPECIES
	+NoTarget
	+NoInFighting
	damagefactor "BFG", 0.05
	SeeSound 	"Tyrant/See"
	PainSound 	"Tyrant/Pain"
	DeathSound 	"Tyrant/See"
	ActiveSound "Tyrant/Idle"
	Obituary "$OB_CYBORG"
	Tag "Tyrant"
	DropItem "SuperHeavyDrop"
	States
	{
	Spawn:
		CYBR A 0 NODELAY
		Goto Idle
	Idle:
		CYBR A 0
		CYBR AA 10 A_Look
		Loop
	See:
		CYBR A 0 A_PlaySound("Doom4/Monsters/Cyberdemon/StepHoof",6,1.0,0,0.9)
		CYBR A 3 A_Chase
		CYBR ABBCC 3 A_Chase
		CYBR A 0 A_PlaySound("Doom4/Monsters/Cyberdemon/StepMech",7,1.0,0,0.7)
		CYBR D 3 A_Chase
		CYBR D 3 A_Chase
		Loop
	//----------------------------------------------------------
	//	Attacks
	//----------------------------------------------------------
	Missile:
		CYBR E 0
		Goto ChooseAnyAttack
	ChooseAnyAttack:
		CYBR E 1 A_Jump(96,"Blade","RocketRain","LaserSelect")
		//CYBR E 1 A_Jump(255,"Blade")
		Goto NormalRockets	
	Slide:
		TNT1 A 0 A_PlaySound("Tyrant/Dodge",7,1.0,False,ATTN_NORM)
	    TNT1 A 0 A_Jump(128, "Slide2")
		RFTR E 0 A_FaceTarget
		CYBR E 0 ThrustThing (angle*256/360+192, 20,0,0)
		CYBR EEEEEEEEEEEEEEEEEE 1 A_SpawnItemEX("BaronFireballTrail",Random(4,-4),Random(4,-4),Random(0,2))
		CYBR EE 2 A_FaceTarget
		Goto NormalRockets+1
	Slide2:
		RFTR E 0 A_FaceTarget
		RFTR E 0 ThrustThing (angle*256/360+64, 20,0,0)
		CYBR EEEEEEEEEEEEEEEEEE 1 A_SpawnItemEX("BaronFireballTrail",Random(4,-4),Random(4,-4),Random(0,2))
		CYBR EE 2 A_FaceTarget
		Goto NormalRockets+1
	//-------------------------
	// 3 strong rockets
	NormalRockets:
			//TNT1 A 0 A_Jump(128,"Slide")
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocket",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocketSmall",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocket",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocketSmall",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR A 0 A_Jump(128,5)
			CYBR F 2 A_CustomMissile("D16CyberdemonRocket",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocketSmall",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocket",56,-22,Random(1,-1))
			CYBR E 2 A_FaceTarget
			CYBR F 2 A_CustomMissile("D16CyberdemonRocketSmall",56,-22,Random(1,-1))
			CYBR EE 2 A_FaceTarget
			Goto See
	RocketRain:
		CYBR E 0 A_JumpifCloser(480,"RocketRainRandom")
		CYBR E 0 A_FaceTarget
		CYBR E 0 A_SetUserVar("user_rocketcount",0)
		Goto RocketRainLoop
	RocketRainLoop:
		CYBR E 4 A_SpawnItemEx("RocketRainUpwards", 0,0,70, frandom(5.0,-5.0),frandom(5.0,-5.0),20, 0,SXF_NOCHECKPOSITION)
		CYBR E 0 A_VileTarget("CyberdemonRainTracker")
		CYBR E 0 A_SetUserVar(user_rocketcount,user_rocketcount+1)
		CYBR E 0 A_JumpIf(user_rocketcount>=15,"EndRocketRain")
		Loop
	EndRocketRain:
		CYBR E 35
		Goto See	
		
	RocketRainRandom:
		CYBR E 0 A_FaceTarget
		CYBR E 0 A_SetUserVar("user_rocketcount",0)
		Goto RocketRainRandomLoop
	RocketRainRandomLoop:
		CYBR E 1 A_SpawnItemEx("RocketRainUpwardsRandom", 0,0,70, frandom(9.0,-9.0),frandom(9.0,-9.0),20, 0,SXF_NOCHECKPOSITION)
		CYBR E 0 A_SpawnItemEx("RocketRainUpwardsRandom", 0,0,70, frandom(9.0,-9.0),frandom(9.0,-9.0),20, 0,SXF_NOCHECKPOSITION)
		//CYBR E 0 A_SpawnItemEx("RocketRainUpwardsRandom", 0,0,70, frandom(4.0,-4.0),frandom(4.0,-4.0),20, 0,SXF_NOCHECKPOSITION)
		CYBR E 0 A_SetUserVar(user_rocketcount,user_rocketcount+1)
		CYBR E 0 A_JumpIf(user_rocketcount>=25,"EndRocketRain")
		Loop

	//-------------------------
	// Laser / railgun attack
	LaserShotMiddle:
		//TNT1 A 0 A_Jump(128,"LaserShotLeft")
		CYBR A 0 A_FaceTarget
		CYBR A 0 A_PlaySound ("Tyrant/Lockon",5,0.5,false,ATTN_NONE)
		CYBR A 0 A_CustomRailgun(0,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_CENTERZ|RGF_EXPLICITANGLE,1,1,"Custompuff",-1,0,4096,1,0,0,None,0)
		CYBR E 5 A_FaceTarget
		CYBR EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_CustomRailgun(0,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_CENTERZ|RGF_EXPLICITANGLE,1,0,"Custompuff",-1,0,2048,1,0,0,None,0)
		CYBR A 0 A_FaceTarget
		CYBR AAAAAAAA 0 A_CustomRailgun(0,-33,"Orange","None",RGF_FULLBRIGHT|RGF_SILENT|RGF_EXPLICITANGLE,1,0,"Custompuff",-1,0,4096,64,0,frandom(1.5,3.0),none,0)
		CYBR A 0 A_SpawnItemEx("CyberdemonArmRocketFX",64,-26,80)
		CYBR E 6 Bright A_CustomRailgun(30,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_EXPLICITANGLE,1,0,"Custompuff",-1,0,4096,1,14,0,"ChargedShot",0)
		TNT1 A 0 A_playSound ("Tyrant/BladeDeath",7,1.0,False,ATTN_NONE)
		CYBR EE 0 A_FaceTarget
		TNT1 A 0 A_SetUserVar(user_laserShot,user_laserShot+1)
		SCRP D 0 A_JumpIf(user_laserShot==3,"LaserFinish")
		Goto LaserSelect
	LaserShotLeft:
		CYBR A 0 A_FaceTarget
		CYBR A 0 A_PlaySound ("Tyrant/Lockon",5,0.5,false,ATTN_NONE)
		CYBR A 0 A_CustomRailgun(0,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_CENTERZ|RGF_EXPLICITANGLE,1,1,"Custompuff",-7,0,4096,1,0,0,None,0)
		CYBR E 5 A_FaceTarget
		CYBR EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_CustomRailgun(0,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_CENTERZ|RGF_EXPLICITANGLE,1,0,"Custompuff",-7,0,2048,1,0,0,None,0)
		CYBR A 0 A_FaceTarget
		CYBR AAAAAAAA 0 A_CustomRailgun(0,-33,"Orange","None",RGF_FULLBRIGHT|RGF_SILENT|RGF_EXPLICITANGLE,1,0,"Custompuff",-7,0,4096,64,0,frandom(1.5,3.0),none,0)
		CYBR A 0 A_SpawnItemEx("CyberdemonArmRocketFX",64,-26,80)
		CYBR E 6 Bright A_CustomRailgun(30,-33,"None","Orange",RGF_FULLBRIGHT|RGF_SILENT|RGF_EXPLICITANGLE,1,0,"Custompuff",-7,0,4096,1,14,0,"ChargedShot",0)
		TNT1 A 0 A_playSound ("Tyrant/BladeDeath",7,1.0,False,ATTN_NONE)
		CYBR EE 0 A_FaceTarget 
		TNT1 A 0 A_SetUserVar(user_laserShot,user_laserShot+1)
		SCRP D 0 A_JumpIf(user_laserShot==3,"LaserFinish")
		Goto LaserSelect
	LaserSelect:
		CYBR EEEE 5 A_FaceTarget
		CYBR E 1 A_Jump (255,"LaserShotMiddle","LaserShotLeft")
		Goto LaserShotMiddle
	LaserFinish:
		CYBR EE 0  A_SetUserVar(user_lasershot,0)
		CYBR EE 10 A_FaceTarget
		Goto See
	Blade:
		CYBR EE 2 A_FaceTarget
		CYBR E 0 A_SpawnItemEx("TyrantBladeChase",128,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		CYBR E 32
		CYBR E 8
		Goto See
	Pain:
		CYBR G 1 A_Pain
		CYBR G 9 A_SetUserVar(user_lasershot,0)
		Goto See
	XDeath:
	Death:
		CYBR H 0 A_Explode(50,32,0,0,32)
		CYBR H 5 A_ChangeFlag(THRUACTORS,true)
		CYBR I 5 A_Scream
		CYBR JKL 5 A_SpawnItemEx("BloodGibDropperBig",0,0,Random(16,28),0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		CYBR M 5 A_NoBlocking
		CYBR NO 6
		CYBR P 30 A_bossdeath
		CYBR P -1
		Stop
	}
}
ACTOR TyrantBladeChase 
{
	var int user_accelerator;
	var int user_timer;
	Health 8000
	PainChance 0
	Speed 20
	Radius 24
	Height 96
	Mass 40000
	Monster
	-Shootable
	+FLOORCLIP
	-Shadow
	Yscale 5.0
	XScale 1.0
	Reactiontime 320
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[3.0,0.6,0.08]"
	SeeSound "Tyrant/BladeActive"
	DeathSound "Tyrant/BladeDeath"
	Obituary "$OB_DEMONHIT"
	States
	{
	Spawn:
		RR28 ABCD 1 BRIGHT A_Look
		Loop
	See:
		TNT1 A 0 A_PlaySound ("Carcass/GroundLoop",7,1.0,false,ATTN_NORM)
		TNT1 A 0 A_SetUserVar(user_timer,0)
		RR28 ABCDABCDABCD 1 BRIGHT A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		Goto Chase
	Chase:
		TNT1 A 0 A_CountDown
		RR28 B 1 BRIGHT A_FastChase
		TNT1 A 0 A_JumpIfCloser(450,"Charge")
		TNT1 A 0 A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		RR28 C 1 BRIGHT  A_FastChase
		TNT1 A 0 A_JumpIfCloser(450,"Charge")
		TNT1 A 0 A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		RR28 D 1 BRIGHT  A_FastChase
		TNT1 A 0 A_JumpIfCloser(450,"Charge")
		TNT1 A 0 A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		RR28 E 1 BRIGHT A_FastChase
		TNT1 A 0 A_JumpIfCloser(450,"Charge")
		TNT1 A 0 A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		TNT1 A 0 A_JumpIfCloser(450,"Charge")
		Loop
	Charge:
		RR28 A 0 A_Recoil(-user_accelerator-2)
		RR28 D 0 A_Recoil(-user_accelerator)
		TNT1 A 0 A_FaceTarget
		RR28 ABCD 1 BRIGHT A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		TNT1 A 0 A_JumpIf(velx==0 && vely==0,"Death")
		TNT1 A 0 A_SetUserVar(user_accelerator,user_accelerator+2)
		RR28 D 0 A_Recoil(-user_accelerator)
		TNT1 A 0 A_FaceTarget
		RR28 ABCD 1 BRIGHT A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		TNT1 B 0 A_JumpIf(velx==0 && vely==0,"Death")
		TNT1 B 0 A_SetUserVar(user_accelerator,user_accelerator+2)
		RR28 D 0 A_Recoil(-user_accelerator)
		TNT1 A 0 A_FaceTarget
		RR28 ABCD 1 BRIGHT A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		TNT1 C 0 A_JumpIf(velx==0 && vely==0,"Death")
		TNT1 C 0 A_SetUserVar(user_accelerator,user_accelerator+2)
		RR28 D 0 A_Recoil(-user_accelerator)
		TNT1 A 0 A_FaceTarget
		RR28 ABCD 1 BRIGHT A_SpawnItemEx("TyrantBladePuff",random(4,-4),Random(4,-4))
		TNT1 D 0 A_JumpIf(velx==0 && vely==0,"Death")
		TNT1 D 0 A_SetUserVar(user_accelerator,user_accelerator+2)
		TNT1 A 0 A_SetUserVar(user_timer,user_timer+1)
		SCRP D 0 A_JumpIf(user_timer==4,"Death")
		Goto Charge+3
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SetScale(1.0,2.0)
		TNT1 A 0 A_playSound ("Tyrant/BladeDeath",7,1.0,False,ATTN_NORM)
		TNT1 A 0 A_Explode(96,128)
		TNT1 A 0 A_Explode(32,64)
		DB27 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright Light("orangeMediumFlicker1") A_SpawnItemEx("TyrantBladePuff",random(8,-8),Random(8,-8))
		Stop
	}
}
ACTOR TyrantBladePuff
{
   Radius 1
   Height 1
   Speed 0
   Yscale 0.9
   XScale 0.3
   RENDERSTYLE ADD
   DamageType fire
   ALPHA 0.67
   PROJECTILE
   ExplosionRadius 8
   ExplosionDamage 4
   Translation "0:255=%[0,0,0]:[3.0,0.6,0.08]"
   +FLOORHUGGER
   -NOGRAVITY
   +DONTSPLASH
   States
   {
   Spawn:
	  TNT1 A 0 A_Jump(256,"Fire1","Fire2","Fire3")
	  Loop
	  Fire1:
      X052 ABCDEFGHIJKLMNOPQR 1 Bright A_SpawnItemEx("TyrantBladePuffSmall",random(6,-6),Random(6,-6))
      stop
	  Fire2:
      X053 ABCDEFGHIJKLMNOPQRST 1 Bright  A_SpawnItemEx("TyrantBladePuffSmall",random(6,-6),Random(6,-6))
      stop
	  Fire3:
      X054 ABCDEFGHIJKLMNOPQRS 1 Bright  A_SpawnItemEx("TyrantBladePuffSmall",random(6,-6),Random(6,-6))
      stop
   }
}

ACTOR TyrantBladePuffSmall
{
   Radius 1
   Height 1
   Speed 0
   Yscale 0.2
   XScale 0.2
   RENDERSTYLE ADD
   DamageType fire
   ALPHA 0.67
   PROJECTILE
   ExplosionRadius 8
   ExplosionDamage 4
   Translation "0:255=%[0,0,0]:[3.0,0.6,0.08]"
   +FLOORHUGGER
   -NOGRAVITY
   +DONTSPLASH
   States
   {
   Spawn:
	  TNT1 A 0 A_Jump(256,"Fire1","Fire2","Fire3")
	  Loop
	  Fire1:
      X052 ABCDEFGHIJKLMNOPQR 1 Bright 
      stop
	  Fire2:
      X053 ABCDEFGHIJKLMNOPQRST 1 Bright 
      stop
	  Fire3:
      X054 ABCDEFGHIJKLMNOPQRS 1 Bright 
      stop
   }
}
	
	
	Actor D16CyberdemonRocket
		{
		Projectile
		+EXTREMEDEATH
		+FORCERADIUSDMG
		Speed 24
		FastSpeed 24
		Radius 6
		Height 12
		Damage (6)
		Scale 0.56
		SeeSound "Cybie/FireRocket"
		DeathSound "Cybie/RocketExplosion"
		States
		{
		Spawn:
		   MICR AAA 1 BRIGHT A_Weave(2,2,1.0,1.0)
		   MICR A 0 BRIGHT A_SpawnItemEx("CyberdemonRocketTrail",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION,64)
		Loop
		Death:
		   TNT1 A 0 A_Explode(3,48,0)
		   TNT1 A 0 A_Quake(2,2,0,512)
		   TNT1 A 0 A_SpawnItemEx("CyberdemonRocketExplosionCloud",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		   FX52 ABCDEFGHIJKLMN 1 BRIGHT
		Stop
		}
		}
	Actor D16CyberdemonRocketSmall : D16CyberdemonRocket
		{
		Speed 18
		FastSpeed 18
		Damage (2)
		Scale 0.35
		States
		{
		Spawn:
		   MICR AAA 1 BRIGHT A_Weave(3,3,1.0,1.0)
		   MICR A 0 BRIGHT A_SpawnItemEx("CyberdemonRocketTrailSmall",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION,64)
		Loop
		Death:
		   TNT1 A 0 A_Explode(1,24,0)
		   TNT1 A 0 A_Quake(2,2,0,512)
		   TNT1 A 0 A_SpawnItemEx("CyberdemonRocketExplosionCloudSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		   FX52 ABCDEFGHIJKLMN 1 BRIGHT
		Stop
		}
		}
		
		
		Actor CyberdemonRocketTrail
		{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Renderstyle ADD
		Alpha 0.7
		Scale 0.7
		States
		{
		Spawn:
		   TNT1 A 0 NoDelay A_Jump(256,"A","B")
		A:
		   X029 ABC 1 BRIGHT
		   X029 DEF 1 BRIGHT A_Fadeout
		Stop
		B:
		   X029 FGH 1 BRIGHT
		   X029 IJK 1 BRIGHT A_Fadeout
		Stop
		}
		}
		
		Actor CyberdemonRocketTrailSmall : CyberdemonRocketTrail
		{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Renderstyle ADD
		Alpha 0.7
		Scale 0.45
		States
		{
		Spawn:
		   TNT1 A 0 NoDelay A_Jump(256,"A","B")
		A:
		   X029 ABC 1 BRIGHT
		   X029 DEF 1 BRIGHT A_Fadeout
		Stop
		B:
		   X029 FGH 1 BRIGHT
		   X029 IJK 1 BRIGHT A_Fadeout
		Stop
		}
		}


		Actor CyberdemonRocketExplosionCloud
		{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Renderstyle ADD
		Alpha 0.7
		Scale 0.7
		States
		{
		Spawn:
		   X112 ABCDE 2 BRIGHT
		   X112 FGH 2 BRIGHT A_Fadeout
		Stop
		}
		}
		
		Actor CyberdemonRocketExplosionCloudSmall : CyberdemonRocketExplosionCloud
		{
		Scale 0.3
		}
		
		Actor CyberdemonArmRocketFX
		{
		+CLIENTSIDEONLY
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Renderstyle ADD
		Alpha 0.8
		Scale 0.55
		States
		{
		Spawn:
		   X112 ABCDE 2 BRIGHT
		   X112 FGH 2 BRIGHT A_Fadeout
		Stop
		}
		}
		//-------------------------------------------------
//Rocket Rain

Actor RocketRainUpwards	//just visual effect
{
	Projectile
	Speed 0
	Radius 3
	Height 2
	+THRUACTORS
	+SKYEXPLODE
	Scale 0.4
	States
	{
	Spawn:
		MICR B 0 NODELAY A_PlaySound("Doom4/Monsters/CybDemShootSwarm",5,1.0,false,0.95)
		MICR B 50 Bright
		Goto Death
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_ChangeFlag(NOINTERACTION,1)
		TNT1 A 50
		Stop
	}
}


Actor CyberdemonRainTracker
{
	+NOINTERACTION
	RenderStyle None
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 0 NODELAY 
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 5 A_Fire
	SpawnStuff:
		TNT1 A 10 A_SpawnItemEx("DummyRainSpawner",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor DummyRainSpawner	//why? because otherwise, it wouldnt spawn the rocket in the ceiling if i tried through the tracker
{
	+NOINTERACTION
	+SPAWNCEILING
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 15 A_CustomMissile("RocketTarget") //waits a bit
		TNT1 A 5 A_CustomMissile("RocketRainingDown") //then drops
		Stop
	}
}

Actor RocketRainingDown
{
	Projectile
	Speed 0
	Radius 3
	Height 5
	+SPAWNCEILING
	+CEILINGHUGGER
	-NOGRAVITY
	Gravity 2.5
	Scale 0.4
	DeathSound "Doom4/Monsters/CybDemSmallRocketExplo"
	States
	{
	Spawn:
		MICR C 1 Bright
		Loop
	Death:
		TNT1 A 0 A_Explode(16,48,0,0,32)
		TNT1 A 0 A_Quake(2,2,0,512)
		TNT1 A 0 A_SpawnItemEx("CyberdemonRocketExplosionCloud",0,0,15,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 10 A_ChangeFlag("NOINTERACTION",1)
		Stop
	}
}
Actor RocketRainingDownStrong : RocketRainingDown
{
	States
	{
	Death:
		TNT1 A 0 A_Explode(32,128,0,0,64)
		TNT1 A 0 A_Quake(2,2,0,512)
		TNT1 A 0 A_SpawnItemEx("CyberdemonRocketExplosionCloud",0,0,15,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 10 A_ChangeFlag("NOINTERACTION",1)
		Stop
	}
}

Actor RocketTarget
{
	Radius 2
	Height 2
	+NOINTERACTION
	+BRIGHT
	+FORCEXYBILLBOARD
	+FLOORHUGGER
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[2.0,0.0,0.2]"
	XScale 0.7
	YScale 0.15
	States
	{
	Spawn:
		X060 A 30 Bright
		X060 A 2 Bright A_FadeOut(0.05)
		Wait

	}
}

Actor RocketZoomer
{
	Radius 2
	Height 2
	+NOINTERACTION
	+BRIGHT
	+FORCEXYBILLBOARD
	+FLOORHUGGER
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[2.0,0.0,0.2]"
	Scale 2.51
	Alpha 0.01
	States
	{
	Spawn:
		X060 A 30 Bright
		Goto Looper
	Looper:
		X060 A 1 Bright A_SetScale(scalex - 0.05)
		X060 A 0 Bright A_FadeTo(2.51, 0.05, true)
		Loop
	}
}


//-------------------------------------------------
//Rocket Rain random

Actor RocketRainUpwardsRandom	//just visual effect
{
	Projectile
	Speed 0
	Radius 3
	Height 8
	+THRUACTORS
	+SKYEXPLODE
	Scale 0.4
	RenderStyle None
	States
	{
	Spawn:
		MICR B 0 NODELAY A_PlaySound("Doom4/Monsters/CybDemShootSwarm",5,1.0,false,0.95)
		MICR B 50 Bright
		Goto Death
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_ChangeFlag(NOINTERACTION,1)
		TNT1 A 50 A_CustomMissile("RRUrandomer",0,0,random(0,359),CMF_TRACKOWNER)
		Stop
	}
}

Actor RRUrandomer
{
	Projectile 
	Radius 3
	Height 3
	Speed 35
	Scale 0.4
	+CEILINGHUGGER
	+SPAWNCEILING
	RenderStyle None
	States
	{
	Spawn:
		TROO A 0 NODELAY A_Jump(200,2,4,6,8,10,12,14)
		TROO AAAAAAAAAAAAAAAAA 1 
		Goto Death
	Death:
		TROO A 0 A_Stop
		TROO A 0 A_ChangeFlag("NOINTERACTION",1)
		TROO A 10 A_SpawnItemEx("DummyRainRandomSpawner",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor DummyRainRandomSpawner	//why? because otherwise, it wouldnt spawn the rocket in the ceiling if i tried through the tracker
{
	+NOINTERACTION
	+SPAWNCEILING
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 30 A_CustomMissile("RocketTarget") //waits a bit
		TNT1 A 10 A_CustomMissile("RocketRainingDownStrong") //then drops
		Stop
	}
}


		// Charged Shot Actors //

		Actor ChargedShotStartup
		{
		Renderstyle ADD
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]"
		Scale 0.0
		Alpha 0.6
		ReactionTime 60
		States
		{
		Spawn:
		   X060 A 1 Bright A_SetScale(scalex+0.01)
		   X060 A 0 A_Warp(AAPTR_TARGET,64,-26,80,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		   X060 A 0 A_CountDown
		   loop
		Death:
		   X060 AAAAA 1 A_SetScale(scalex/2)
		Stop
		}
		}

		Actor ChargedShotStartup2
		{
		Renderstyle ADD
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]"
		Scale 0.0
		Alpha 0.6
		ReactionTime 60
		States
		{
		Spawn:
		   X072 A 1 BRIGHT A_SetScale(scalex+0.01)
		   X072 A 0 A_Warp(AAPTR_TARGET,64,-26,80,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		   X072 A 0 A_CountDown
		   loop
		Death:
		   X072 AAAAA 1 BRIGHT A_SetScale(scalex/2)
		Stop
		}
		}

		Actor ChargedShot
		{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		RenderStyle Add
		Scale 0.55
		States
		{
		Spawn:
		   X025 B 8 BRIGHT 
		   X025 B 1 BRIGHT A_FadeOut(0.06)
		   Stop
		}
		}

		Actor Custompuff
		{
		+NOINTERACTION
		+BLOODLESSIMPACT
		Projectile
		States
		{
		Spawn:
		   TNT1 A 4
		   Stop
		}
		}

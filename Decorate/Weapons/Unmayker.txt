ACTOR UMKSelected : EOA_Token{}

ACTOR EOA_Unmayker : EOA_Weapon
{
	Inventory.Icon "ICONBFG"
	Weapon.AmmoType "EOA_BFGAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 60
	Inventory.PickupMessage "Unmayker"
	Tag "BFG-9000"
	+BFG
	+POWERWEAPON
	States
	{
	Spawn:
		UMKW A -1
		Stop
	Select:
		TNT1 A 0
		{
			A_WeaponOffset(0, 32);
			//A_SpawnNoiseMaker("BFG/Idle", 6, 0.25, 1);
			SetPlayerProperty(0, 1, PROP_INSTANTWEAPONSWITCH);
			A_GiveInventory("UMKSelected");
			ACS_NamedExecuteAlways("BFGReticle", 0);
			A_SetCrosshair(99);
			A_ZoomFactor;
		}
		UMKI A 1 A_Raise(60)
		Wait 
	Deselect:
		TNT1 A 0 A_ZoomFactor
		UMKI A 1 EOA_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH, EOA_WEAPSPEEDUPGRADE * CountInv("Adept"))
		TNT1 A 0
		{
			A_TakeInventory("UMKSelected");
			SetPlayerProperty(0, 0, PROP_INSTANTWEAPONSWITCH);
		}
		TNT1 A 4 A_SetTics(4 * !CountInv("Adept") * !CountInv("PowerBerserk"))
		UMKI A 1 A_Lower(36)
		Wait
	Ready:
		TNT1 A 0
		{
			A_SpawnNoiseMaker("Weapon/Switch");
			A_SpawnNoiseMaker("BFG/Raise", 5, 0.5);
		}
		UMKI A 1 EOA_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_DISABLEHASTE|EOA_WEAPSPEEDUPGRADE * CountInv("Adept"))//ABCDEFGH 1
		Goto ReadyLoop
	ReRaise:
		UMKR ABCDEFGH 1 EOA_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_DISABLEHASTE)
		//"####" "#" 0 A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH|WRF_ALLOWRELOAD|WRF_ALLOWZOOM * !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ZOOM))
	ReadyLoop:
		UMKI A 1 EOA_WeaponReady(WRF_NOFIRE * (GetPlayerInput(INPUT_OLDBUTTONS) & BT_ATTACK)|WRF_ALLOWZOOM * !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ZOOM), EOA_ALLOWMANTLE|EOA_ALLOWEQUIPMENT)
		Loop
	Fire:
		TNT1 A 0 
		{
			A_AlertMonsters;
		}
		UMKF AB 2 EOA_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0
		{
			A_PlaySound("UNMK/fire");
			EOA_FireBullets(0, 0, -1, 0, "EOA_NullPuff", FBF_NORANDOMPUFFZ|FBF_USEAMMO, 32767, "EOA_Unmaykertracer", -11 + GetViewZ, 5 + GetRelVelY / 3);
			EOA_FireBullets(-10, 0, -1, 0, "EOA_NullPuff", FBF_NORANDOMPUFFZ, 32767, "EOA_Unmaykertracer", -11 + GetViewZ, 5 + GetRelVelY / 3);
			EOA_FireBullets(10, 0, -1, 0, "EOA_NullPuff", FBF_NORANDOMPUFFZ, 32767, "EOA_Unmaykertracer", -11 + GetViewZ, 5 + GetRelVelY / 3);
			EOA_SetWeaponRecoilOffset(frandompick(-0.5, 0.5), frandompick(-0.5, 0.5), 0, 0.25);
			EOA_SpawnCustomDynamicLight("PulseLight", 0.7, 0.2, 0.2, 64, 0, 8, 1, DYN_ADDITIVE);
			EOA_SpawnCustomDynamicLight("PulseLight", 0.7, 0.2, 0.2, 64, 0, 8, 1, DYN_ATTENUATE);
			If (CountInv("PowerQDMG"))
			{
				A_SpawnNoiseMaker("QuadDamage/Shot", 4, 1.0, 0, ATTN_NORM, 0, 18); //A_GiveInventory("QuadDamageShot");
			}
		}
		UMKF B 1 A_ReFire
		UMKF C 2
		TNT1 A 0 
		{
			If (!(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK) || CountInv("Grabbing_A_Ledge") || (CountInv("EquipmentTrigger") && CountInv("EquipmentCharge")))
			{
				Return State ("ReadyLoop");
			}
			Else If (GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK && CountInv("EOA_BFGAmmo"))
			{
				Return State ("Fire");
			}
			Return State ("");
		}
		Goto ReadyLoop
	Zoom:
		BFGT A 0
		Goto Super::Melee
	UseEquipment:
		"####" "#" 0
		Goto Super::UseEquipment
	}
}

ACTOR EOA_Unmaykertracer : EOA_Projectile
{
	Projectile
	Scale 2
	Radius 9
	Height 6
	Speed 50
	Damage (130)
	RenderStyle "Add"
	Decal "UnmakyrScorch"
	+BLOODLESSIMPACT
	+EXTREMEDEATH
	+FORCERADIUSDMG
	+SKYEXPLODE
	States
	{
	Spawn:
		TRAC A 0 NoDelay
		{
			If (CountInv("PowerQDMG", AAPTR_TARGET))
			{
				A_SpawnItemEx("QuadDamageFX2", 0, 0, 0, 0, 0, 0, 0, SXF_ORIGINATOR|SXF_TRANSFERPOINTERS|SXF_TRANSFERSCALE|SXF_SETTRACER|SXF_NOCHECKPOSITION);
			}
		}
		TRAC A 1 bright
		{
			EOA_SpawnCustomDynamicLight("PointLight", 1, 0.2, 0.1, 5, 0, 1, 1, DYN_ADDITIVE);
		}
		Wait
	Crash:
		TNT1 A 1
		{
			A_PlaySound("Projectile/HitWall");
			EOA_SpawnCustomDynamicLight("PointLight", 1, 0.6, 0.3, 8, 0, 4, 1, DYN_ADDITIVE, False);
			EOA_SpawnCustomDynamicLight("PointLight", 1, 0.6, 0.3, 8, 0, 4, 1, DYN_ATTENUATE, False);
		}
		Stop
	Death:
	XDeath:
		TNT1 A 1
		{
			A_PlaySound("Projectile/HitFlesh");
			EOA_SpawnCustomDynamicLight("PointLight", 1, 0.6, 0.3, 8, 0, 4, 1, DYN_ADDITIVE, False);
			EOA_SpawnCustomDynamicLight("PointLight", 1, 0.6, 0.3, 8, 0, 4, 1, DYN_ATTENUATE, False);
		}
		Stop
	}
}

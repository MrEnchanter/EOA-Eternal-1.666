#library "CACOBALL"
#include "zcommon.acs"

#define CACOBALL_ID_BELOW 	2999
#define CACOBALL_ID_MAX 	3127
#define BLOOD_ID_BELOW 	3127
#define BLOOD_ID_MAX 	3511
#define SMOKE_ID_BELOW 	3511
#define SMOKE_ID_MAX 	3639
#define GLITCH_ID_BELOW	3639
#define GLITCH_ID_MAX 	4091

int cacospot=CACOBALL_ID_BELOW;
script "D4T Cacoball" (void)
{
	if(GameType() == GAME_SINGLE_PLAYER) terminate;
	
	str filename = strparam(s:"cacohit", d:random(1,4));
	
	SetFont(filename);
	HudMessage(s:"A"; HUDMSG_FADEOUT|HUDMSG_ALPHA , ++cacospot, 0, 
			random(-2.0,2.0), random(-2.0,2.0), 
			1.0, 8.0, 0.85); 
	
	if(cacospot > CACOBALL_ID_MAX)  cacospot = CACOBALL_ID_BELOW;
	if(cacospot < CACOBALL_ID_BELOW)cacospot = CACOBALL_ID_BELOW;
}
script "D4T Arachnoball" (void)
{
	str filename = strparam(s:"arachit", d:random(1,8));
	
	SetFont(filename);
	HudMessage(s:"A"; HUDMSG_FADEOUT|HUDMSG_ADDBLEND|HUDMSG_ALPHA, ++cacospot, CR_UNTRANSLATED, 
			random(-2.0,2.0), random(-2.0,2.0), 
			0.05, 0.15, 0.99); 
	
	if(cacospot > CACOBALL_ID_MAX)  cacospot = CACOBALL_ID_BELOW;
	if(cacospot < CACOBALL_ID_BELOW)cacospot = CACOBALL_ID_BELOW;
}

// I'll leave interpreting name of this variable as an exercise for reader.
int smokespot=SMOKE_ID_BELOW; 
script "D4T Blinding Smoke" (void)
{
	str filename = strparam(s:"smoke_", d:random(0,1), d:random(0,7));
	
	++smokespot;
	
	SetFont(filename);
	HudMessage(s:"A"; 
			HUDMSG_FADEINOUT|HUDMSG_ALPHA, smokespot, CR_UNTRANSLATED, 
			random(-2.0,2.0), random(-2.0,2.0), 
			0.5, random(0.1,1.0), random(2.0,random(3.0, 7.0)), 0.99); 
	
	if(smokespot > SMOKE_ID_MAX)  smokespot = SMOKE_ID_BELOW;
	if(smokespot < SMOKE_ID_BELOW)smokespot = SMOKE_ID_BELOW;
}

script "D4T Clear Screen" (void)
{
	int i; str fromwhat = "";
	delay(1);
	for(i = CACOBALL_ID_BELOW + 1; i <= CACOBALL_ID_MAX; i++)
	{
		clearMessage(i);
		fromwhat = strparam(s:fromwhat, s:", ", d:i);
	}
	fromwhat = "";
	for(i = SMOKE_ID_BELOW + 1; i <= SMOKE_ID_MAX; i++)
	{
		clearMessage(i);
		fromwhat = strparam(s:fromwhat, s:", ", d:i);
	}
}

function void clearMessage(int id)
{
	HudMessage(s:" "; HUDMSG_PLAIN, id, CR_UNTRANSLATED, 0, 0, 0, 0);
}

// Graphic name, fade in time, fade out time
str BloodProg[16][3]={
		{"blddroq", 0.02, 5.0}, //  1
		{"blddrob", 0.04, 4.0}, //  2
		{"speckle", 0.06, 6.0}, //  3
		{"blddroq", 0.08, 4.5}, //  4
		{"blddrop", 0.10, 3.5}, //  5
		{"bldspry", 0.16, 3.5}, //  6
		{"blddrob", 0.14, 3.0}, //  7
		{"speckle", 0.08, 2.5}, //  8
		{"blddrob", 0.06, 3.0}, //  9
		{"blddroq", 0.08, 6.0}, // 10
		{"blddrop", 0.19, 2.0}, // 11
		{"blddrop", 0.15, 1.5}, // 12
		{"bldspry", 0.28, 1.0}, // 13
		{"blddrop", 0.16, 1.0}, // 14
		{"bldmist", 0.14, 5.5}, // 15
		{"speckle", 0.12, 5.0}, // 16
		};

int bloodspot = BLOOD_ID_BELOW;
int lastsound;
#define TIME_BETWEEN_PAINS 4

script "D4T Blood" (int degree)
{
	if(degree <  1) terminate;
	if(degree > 16) degree = 16;
	
	if(PlayerNumber() < 0) // For pukename to work
		SetActivator(0,AAPTR_TARGET);
	
	int radius = random(-0.99, -0.01);
	int sel = random(0,3);
	
	bool x; // type mismatch 
	str y;  // but who cares in ACS, right
	
	switch(sel)
	{
	case 1:
		x = -0.01;
		y = radius;
		break;
	case 2:
		x = -0.99;
		y = radius;
		break;
	case 3:
		y = -0.01;
		x = radius;
		break;
	default:
		y = -0.99;
		x = radius;
		break;
	}
	
	for(int i=0; i < degree; i++)
	{
		int filename = strparam(s:BloodProg[i][0], d:random(1,4));
		SetFont(filename);
		HudMessage(s:"A"; HUDMSG_FADEINOUT|HUDMSG_ALPHA , ++bloodspot, 0, 
				x+random(random(-0.2,0.04),random(-0.04, 0.2)), 
				y+random(random(-0.2,0.04),random(-0.04, 0.2)), 
				1.0, BloodProg[i][1], BloodProg[i][2], 0.85); 
	}
	if(bloodspot > BLOOD_ID_MAX)  bloodspot = BLOOD_ID_BELOW;
	if(bloodspot < BLOOD_ID_BELOW)bloodspot = BLOOD_ID_BELOW;
	
/*	//pain sound
	if(lastsound + TIME_BETWEEN_PAINS <= Timer())
	{
		PlaySound (0, "Doom4/Player/PainBeep", CHAN_VOICE, 1.0, false, 100.0);
		lastsound = Timer();
	}*/
}

int glitchspot = GLITCH_ID_BELOW;

script "D4T Glitch" (int degree)
{
	if(degree <  1) terminate;
	if(degree > 16) degree = 16;
	
	if(PlayerNumber() < 0) // For pukename to work
		SetActivator(0,AAPTR_TARGET);
		
	int x, y;
	int hi = 0;
	
	if(degree > 7) hi = random(0,1);
	int lo = 0;
	lo = random(0,random(degree/2, 9));
	for(int i=0; i < degree; i++)
	{
		int filename = strparam(s:"artifa", d:hi, d:lo);
		SetFont(filename);
		HudMessage(s:"A"; HUDMSG_FADEOUT, ++glitchspot, 0, 
				random(-2.0,2.0),random(-2.0,2.0),
				0.04, random(0.01, random(0.02,0.2))); 
	}
	if(glitchspot > GLITCH_ID_MAX)  glitchspot = GLITCH_ID_BELOW;
	if(glitchspot < GLITCH_ID_BELOW)glitchspot = GLITCH_ID_BELOW;
	
	// Armored pain sound
	if(lastsound + TIME_BETWEEN_PAINS <= Timer())
	{
		PlaySound (0, "Doom4/Player/PainBeep", CHAN_VOICE, 1.0, false, 100.0);
		lastsound = Timer();
	}
}
Class EOA_ExplosiveBarrel : ExplosiveBarrel
{
	Int CountDownTimer;
	Bool Died, CountDown;
	Default
	{
		Health 8;
		PushFactor 1.5;
		ReactionTime 525;
		Scale 0.6;
		+DONTTHRUST
		+NOBLOOD
		+PUSHABLE
	}
	Override Void Tick()
	{
		If (CountDown)
		{
			If (CountDownTimer-- <= 0 && !Died)
			{
				Died = True;
				SetState(ResolveState("Death"));
			}
		}
		Super.Tick();
	}
	Override Int DamageMobj(Actor Inflictor, Actor Source, Int Damage, Name MOD, Int Flags, Double Angle)
	{
		CountDown = True;
		If (Inflictor Is "EOA_ExplosiveBarrel" || Inflictor is "WeakForcePush")
		{
			Return 0;
		}
		CountDownTimer = 1;
		Return Super.DamageMobj(Inflictor, Source, Damage, MOD, Flags, Angle);
	}
	Override Int DoSpecialDamage(Actor Target, Int Damage, Name DamageType)
	{
		If (Target.Player)
		{
			Damage /= 2;
		}
		Return Damage;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			Return A_Jump(256, random(1, 3));
		}
		EXPB CBA 0 A_Jump(256, "SpawnLoop");
	SpawnLoop:
		EXPB "#" -1 NoDelay
		{
			CountDownTimer = 15;
		}
		Stop;
	/*Death:
		TNT1 A 1050
		{
			CountDown = False;
			A_Explode(160, 224, 1, 1, 100);
			A_StartSound("Barrel/Explode", 1, 0, 0.75);
			A_StartSound("Barrel/Add", 2, 0, 0.75);
			A_StartSound("Barrel/Debris", 3);
			A_SpawnItemEx("EOA_ExplosiveBarrelExplosionFX");
		}
		TNT1 A 5 A_Respawn();
		Wait;*/
	Death:
		TNT1 A 35
		{
			PlayerInfo Player = Players[ConsolePlayer];
			If(Player.Mo.FindInventory("ExplosivePinatas"))
			{
				A_DropItem("TempChainsawShellAmmo");
				A_DropItem("TempChainsawBulletAmmo");
				A_DropItem("TempChainsawRocketAmmo");
				A_DropItem("TempChainsawCellAmmo");				
			}
			CountDown = False;
			A_Explode(160, 224, 1, 1, 100);
			A_StartSound("Barrel/Explode", 1, 0, 0.75);
			A_StartSound("Barrel/Add", 2, 0, 0.75);
			A_StartSound("Barrel/Debris", 3);
			A_SpawnItemEx("EOA_ExplosiveBarrelExplosionFX");
			bPUSHABLE = False;
			If(!Player.Mo.FindInventory("SelfPreservation"))
			{
				A_BarrelDestroy();
			}
		}
	DeathLooper:
		TNT1 A 1
		{
			ReactionTime++;
			If(ReactionTime >= 35)
			{Return ResolveState("RespawnBarrel");}
			Return ResolveState(Null);
		}
		Loop;
	RespawnBarrel:
		TNT1 A 70 
		{
			Actor SpawnFx = Spawn("SpawnFx1",Pos);
			SpawnFx.SetShade("FF3030"); 
			SpawnFx.A_SetRenderStyle(1.00,STYLE_AddShaded);
		}
		TNT1 A 1 
		{	
			A_SpawnItemEx("EOA_ExplosiveBarrel");
			A_BarrelDestroy();
		}
		Stop;
	}
}

Class EOA_ExplosiveBarrelSpawnFX : Actor
{
	Default
	{
		+BRIGHT
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 0
		{
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "BarrelLight", DynamicLight.PulseLight, (255, 76, 76), 128, 1, DYNAMICLIGHT.LF_ADDITIVE, 10, (0, 0, 0), 0.33);
		}
		Stop;
	}
}

Class SpawnFx1 : Actor
{
	Int LifeTime;
	default
	{
		Radius 1;
		Height 1;
		Gravity 0.66;
		Scale 0.4;
		Alpha 0.05;
		RenderStyle "Add";
		+BRIGHT
		+DONTSPLASH
		+FLATSPRITE
		+NOINTERACTION
		+NOTRIGGER
		+THRUACTORS
	}
	States
	{
	Spawn:
		BSW1 B 1 A_FadeIn(0.25);
	SpawnLoop:
		BSW1 B 1 NoDelay 
		{
			A_SetAngle(Angle+10);
			Invoker.LifeTime++;
			If(Invoker.LifeTime > 70)
			{Return ResolveState("Fade");}
			Return ResolveState(Null);
		}
		Loop;
	Fade:
		"####" B 0 A_SpawnItemEX("GaussImpactFX");
		"####" BBBBBBBBBB 0 
		{
			A_SpawnProjectile("GaussCannonImpactSpark", 5, 0, Random(1, 360), CMF_AIMDIRECTION, random(-90, 45));
		}
		"####" "#" 1
		{
			A_SetScale(Scale.X - 0.01);
			A_FadeOut(0.1);
		}
		Wait;
	}
}

Class EOA_ExplosiveBarrelExplosionFX : Actor
{
	Default
	{
		+BRIGHT
		+NOINTERACTION
	}
	States
	{
	Spawn:
		DB27 A 0 NoDelay A_Jump(128, 2);
		DB28 A 0;
		"####" "#" 0
		{
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "BarrelLight", DynamicLight.PulseLight, (255, 153, 76), 128, 1, DYNAMICLIGHT.LF_ADDITIVE, 10, (0, 0, 0), 0.33);
		}
		"####" ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
		Stop;
	}
}
